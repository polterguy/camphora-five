## Manually editing your app

When you have generated your app, you can use Hyper IDE to edit your app's files. If your app's name is
for instance _"address-book"_, then you can find your app's files in the folder _"/modules/address-book/"_.

**Warning** - If you edit your app, for then to later re-generate your app, then your changes will be lost, since
the generating process overwrites all files from your app's folder.

Your app's structure is the same as any other module you have in your Phosphorus Five installation. The
actual process of editing your app though, is beyond the scope of this documentation, and requires you to
understand Hyperlambda. However, for your convenience, I have included a snippet of Hyperlambda below, that
allows you to see all the possible icons you can use for your app. If you want to change your app's icon,
you can evaluate the snippet below, for then to edit your app's _"desktop.hl"_ file, and change the parts
of your `class` property that declares which icon to use.

T.H. Rose Home Cloud, Inc. can also help you edit your app, or create custom apps entirely from scratch for you,
if you require additional features, or custom apps. This is a service we charge for. Feel free to
send us an email at thomas@gaiasoul.com if you want to hear more about these types of services.

```hyperlambda-snippet
/*
 * Loading HTML example file for IcoMoon.
 */
load-file:"/modules/micro/media/fonts-demo.html"

/*
 * Semantically iterating each "icon-x" class from HTML document retrieved above,
 * and adding one "li" element for each into our [create-widgets] invocation below.
 */
html2lambda:x:/@load-file/*?value
for-each:x:@"/@html2lambda/**/\@class/""=:regex:/icon-.{1,}/""?value"
  eval-x:x:/+/*/*/*/*/*
  add:x:/../*/create-widgets/**/ol/*/widgets
    src
      container
        element:li
        widgets
          span
            innerValue:
            class:x:/@_dp?value
          span
            innerValue:" - "
          span
            innerValue:x:/@_dp?value

/*
 * Creating a widget wrapping each icon from IcoMoon.
 */
create-widgets
  micro.widgets.modal:dox-icons-modal
    widgets
      div
        class:air
        widgets
          h3
            innerValue:Icons
          ol
            widgets
      div
        class:right
        widgets
          button
            innerValue:Close
            onclick
              delete-widget:dox-icons-modal
```

Below is an example of a _"desktop.hl"_ file from a Camphora Five generated app. If you change the `icon-paragraph-justify`
parts of your **[class]** declaration to any of the values that you found by evaluating the above Hyperlambda snippet,
your app's desktop icon will change. **Notice**, it's important that you keep the _"desktop-app-icon"_ parts
unchanged of your class name declaration.

```hyperlambda
/*
 * Notice,the [href] parts of this anchor Hyperlink is automatically
 * generated by the "desktop" module.
 */
container
  element:a
  role:button
  class:desktop-app button shaded
  widgets
    literal
      element:span

      /*
       * Change the "icon-paragraph-justify" parts below to any
       * of the values found by evaluating the above Hyperlambda.
       */
      class:icon-paragraph-justify desktop-app-icon

    literal
      element:span
      class:desktop-app-text capitalize

      /*
       * Change the "address-book" parts below, if you want to
       * give a different name to your app.
       */
      innerValue:address-book
```

### Camphora Five's internals

To understand how to manually edit your app, it might help to understand some of the internals behind Camphora Five.
Inside Camphora Five's main modules folder, which you can find in _"/modules/camphora-five/"_, using for instance
Hyper IDE - There is a folder called _"/template/"_. This folder will be used as a template folder to create a plain
and normal Phosphorus Five module. The template folder again will contain most of the default files necessary to wire
up your Phosphorus Five application, such as a _"desktop.hl"_ file, a _"launch.hl"_ file, etc.

After your app has been generated, there will exist a file inside of your _"/helpers/"_ folder, inside of your CRUD
app, which is called _"app-manifest.hl"_. This file actually declares the columns and settings for your app, and it
should be fairly self explaining. However, you can actually modify this file by hand if you wish to, which eliminates
the need to re-generate your app. This allows you to change your app's columns, views and settings, without having
to _"re-generate"_ your app, and loose your manually edited changes.

Your Camphora Five app though, is dependent upon some helper services, which is supplied by Camphora Five itself. So
your app will not function, if you for some reasons uninstall Camphora Five. These helper services, provides
functionality such as transforming Markdown to HTML, etc. So hence your app is created from a _"template"_, which
you can edit as you see fit, after you have initially generated your app. This allows you to create a Camphora CRUD
app, which you edit after having generated it, to your exact needs, as you see fit. Just remember a crucial
detail - **If you re-generate your app, these changes will be overwritten!!**

#### About Markdown and Multiline fields

When Markdown for multiline text elements is created, Camphora Five will load your _"html-whitelist.hl"_ file, for your
app, inside of your app's _"/configuration/"_ folder. This allows you to modify the legal HTML elements for your
particular app. Markdown is created as a _"leaky format"_, which allows you to create HTML tags inside
your Markdown. These tags will be rendered _"as is"_ in your resulting HTML. However, your HTML
whitelist file, declares which HTML elements and attributes of HTML elements are legal to render here - But only for your
specific app, and not all apps in general.


