/*
 * This is our main "landing page" for our Camphora Five's help files.
 */
h2
  innerValue:Camphora Five

img
  style:"margin-left:1rem; float:right;max-width: 25%;"
  src:"https://phosphorusfive.files.wordpress.com/2018/03/2000px-applications-database-svg.png"

p
  innerValue:@"Camphora Five is a CRUD app generator, that allows you to create some fairly rich web apps, without requiring you to
learn any programming. If all you need to do is to create a web app that allows you to collect, edit, and administrate single table
database data, then Camphora Five should probably be your starting point."

p
  innerValue:@"Apps created with Camphora Five also provide an excellent start for your own apps, and Camphora
also allows you to create your own <em>'views'</em>, completely overriding the default GUI or logic. Camphora
also contains a fairly rich API, if you want to extend it, or build upon apps created with it."


/*
 * All files at "root" of sub section.
 */
container
  element:ul
  oninit

    /*
     * Retrieving all other Hyperlambda files in the help folder.
     */
    list-files:@CAMPHORA/help-files/Camphora Five/
      filter:.hl|.md

    /*
     * Removing "index.hl".
     */
    set:x:@"/@list-files/*/"":regex:/index.hl$/"""

    /*
     * Looping through each Hyperlambda file (except of course "index.hl").
     */
    for-each:x:/@list-files/*

      /*
       * Figuring out filename, without extension, and removing numbering parts.
       */
      split:x:/@_dp/#?name
        =:/
        =:.
      replace:x:/@split/0/-2?name
        src:regex:/[0-9]+ - /

      /*
       * Creating a list item, containing a hyperlink button, which once clicked,
       * loads and displays the specified help file.
       */
      create-widget
        parent:x:/../*/_event?value
        element:li
        widgets
          a
            href:#
            innerValue:x:/@replace?value
            .file:x:/@_dp/#?name
            onclick

              /*
               * Retrieving file name, loading and displaying file, by invoking common
               * widget lambda event, which does the heavy lifting for us.
               */
              get-widget-property:x:/../*/_event?value
                .file
              desktop.help.display-file:x:/@get-widget-property/*/*?value
