/*
 * Creates our __[camphora.widgets.\_internals.view-definition]__ extension widget,
 * that allows the user to create/edit a Camphora Five view. Pass in __[name]__
 * as the name for your view.
 *
 * Optionally pass __[value]__ to edit an existing view, instead of creating a new one.
 *
 * This widget is used when editing your Camphora Five app.
 */
create-event:camphora.widgets._internals.view-definition

  /*
   * Sanity checking arguments.
   */
  micro.lambda.contract.min:x:/..
    name:string
  micro.lambda.contract.optional:x:/..
    value:string

  /*
   * Returns widget to caller.
   */
  eval-x:x:/+/**/value
  return
    container
      .view
      widgets
        div
          class:strip fill
          widgets
            label
              innerValue:Name
            input
              type:text
              value:x:/../*/name?value
              .data-field:view-name
            button
              innerValue:@"<span class=""icon-bin""></span>"
              title:Delete view
              class:large
              onclick

                /*
                 * Since views are anyways dynamically fetched during save, all
                 * we need to do, is to delete widget wrapping view.
                 */
                p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                  .view
                delete-widget:x:/-/*/*?value
                micro.windows.info:View deleted
                  class:micro-windows-info success

        micro.widgets.codemirror
          value:x:/../*/value?value
          .data-field:view-content
