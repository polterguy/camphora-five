/*
 * Creates the __[camphora.widgets.\_internals.field-definition]__ widget, that will
 * create a widget allowing the user to edit the attributes of his field(s).
 *
 * Optionally pass in __[name]__, __[type]__, __[options]__ and __[show]__ to
 * edit an existing field, instead of creating a new one.
 *
 * This widget is used when editing your Camphora Five app.
 */
create-event:camphora.widgets._internals.field-definition

  /*
   * Signal.
   */
  .signal

  /*
   * Parametrizing widget.
   */
  if:x:/../*/type?value
    add:x:/../*/return/**/select/*/widgets/*/option/*/value/={0}/.
      :x:/../*/type?value
      src:selected
    if:x:/../*/type?value
      =:radio
      or:x:/../*/type?value
        =:select
      not
      add:x:/../*/return/**/input/*/.data-field/=options/.
        src
          visible:false
  else-if:x:/../*/type?value
    not
    add:x:/../*/return/**/input/*/.data-field/=options/.
      src
        visible:false
  if:x:/../*/show?value
    =:bool:false
    set:x:/../*/return/**/.data-field/=show-in-grid/./*/checked

  /*
   * If [name] is not given, we set focus to name widget.
   */
  if:x:/../*/name
    not
    add:x:/../*/return/**/.data-field/=field-name/.
      src
        oninit
          micro.page.set-focus:x:/../*/_event?value

  /*
   * Returning widget to caller.
   */
  eval-x:x:/+/**/value
  return
    container
      widgets
        container
          class:row
          widgets
            container
              class:col-50
              widgets
                container
                  class:strip fill
                  widgets
                    label
                      innerValue:Name
                      class:description
                    input
                      type:text
                      placeholder:Name ...
                      title:Name of field
                      .data-field:field-name
                      value:x:/@.signal/--/name?value
                    label
                      title:If the field shall be displayed in the grid view or not.
                      widgets
                        input
                          type:checkbox
                          .data-field:show-in-grid
                          checked
                        span
                          innerValue:Grid
            container
              class:col-50
              widgets
                container
                  class:strip fill
                  widgets
                    label
                      innerValue:Type
                      class:description
                    select
                      .data-field:field-type
                      title:Type of field
                      onchange

                        /*
                         * Retrieving our "options" textbox.
                         */
                        p5.web.widgets.get-parent:x:/../*/_event?value
                        p5.web.widgets.find:x:/-/*/*?value
                          .data-field:options

                        /*
                         * Checking if we should enable our "options" textbox.
                         */
                        get-widget-property:x:/../*/_event?value
                          value
                        if:x:/@get-widget-property/*/*?value
                          =:select
                          or:x:/@get-widget-property/*/*?value
                            =:radio

                          /*
                           * Enabling our "options" textbox.
                           */
                          set-widget-property:x:/@p5.web.widgets.find/*/*?value
                            visible:true

                        else

                          /*
                           * Disabling our "options" textbox.
                           */
                          set-widget-property:x:/@p5.web.widgets.find/*/*?value
                            visible:false

                      widgets
                        option
                          innerValue:Singleline text
                          value:text
                        option
                          innerValue:Multiline text
                          value:textarea
                        option
                          innerValue:Markdown content
                          value:markdown
                        option
                          innerValue:Checkbox
                          value:checkbox
                        option
                          innerValue:Number
                          value:number
                        option
                          innerValue:Date
                          value:date
                        option
                          innerValue:Date time
                          value:datetime
                        option
                          innerValue:Select dropdown
                          value:select
                        option
                          innerValue:Radiobuttons
                          value:radio
                        option
                          innerValue:Date/time created
                          value:created
                        option
                          innerValue:Created by
                          value:user
                    input
                      type:text
                      placeholder:Options ...
                      title:Options, separated by comma
                      .data-field:options
                      value:x:/@.signal/--/options?value
                    input
                      type:hidden
                      .data-field:.signal
                    button
                      innerValue:@"<span class=""icon-bin""></span>"
                      title:Delete field
                      onclick

                        /*
                         * Retrieving parent widget, and simply deleting it.
                         */
                        p5.web.widgets.get-parent:x:/../*/_event?value
                        p5.web.widgets.get-parent:x:/-/*/*?value
                        p5.web.widgets.get-parent:x:/-/*/*?value
                        p5.web.widgets.get-parent:x:/-/*/*?value
                        delete-widget:x:/-/*/*?value
