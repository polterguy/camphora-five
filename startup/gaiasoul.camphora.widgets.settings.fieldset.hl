
/*
 * Creating Camphora fieldset extension widget.
 *
 * Creates the "fieldset help" widget Active Event.
 * Creates a fieldset, with help text, and an icon for toggling the help text. Passs in children widgets to your
 * fieldset widget as [widgets].
 *
 * Arguments;
 *
 * [legend] - Text for legend.
 * [help] - Help text.
 * [widgets] - Widgets in fieldset.
 */
create-event:gaiasoul.camphora.widgets.fieldset

  /*
   * Applying arguments.
   */
  add:x:/../*/return/*/fieldset/*/widgets
    src:x:/../*/widgets/*
  eval-x:x:/../*/return/*/fieldset/*/widgets/*(/legend/*/widgets/*/span/*/innerValue|/literal/*/innerValue)

  /*
   * Returning fieldset widget.
   */
  return
    fieldset
      class:prepend-top
      widgets
        legend
          widgets
            span
              innerValue:x:/../*/legend?value

            /*
             * Button that toggles help text.
             */
            a
              href:#
              role:button
              innerValue:@"<span class=""glyphicon glyphicon-question-sign camphora-fieldset-help-button""></span>"
              onclick

                /*
                 * Toggling visibility of "help text".
                 */
                p5.web.widgets.get-parent:x:/../*/_event?value
                p5.web.widgets.get-parent:x:/-/*/*?value
                p5.web.widgets.find-like:x:/-/*/*?value
                  class:camphora-fieldset-help-info
                if
                  fetch:x:/0/0/0?value.bool
                    p5.web.widgets.property.get:x:/@p5.web.widgets.find-like/*/*?value
                      visible

                  /*
                   * Hiding help text.
                   */
                  p5.web.widgets.property.set:x:/@p5.web.widgets.find-like/*/*?value
                    visible:false
                else

                  /*
                   * Showing help text.
                   */
                  p5.web.widgets.property.set:x:/@p5.web.widgets.find-like/*/*?value
                    visible:true

        /*
         * Help information.
         */
        literal
          element:div
          class:col-xs-12 fade-in prepend-bottom camphora-fieldset-help-info
          visible:false
          innerValue:x:/../*/help?value
