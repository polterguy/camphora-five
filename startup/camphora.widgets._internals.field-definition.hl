
/*
 * Creates our "field definition widget".
 *
 * Optionally pass in [name], [type], [options] and [show]
 */
create-event:camphora.widgets._internals.field-definition

  /*
   * Signal.
   */
  .signal

  /*
   * Parametrizing widget.
   */
  if:x:/../*/type?value
    add:x:/../*/return/**/select/*/widgets/*/option/*/value/={0}/.
      :x:/../*/type?value
      src:selected
    if:x:/../*/type?value
      =:radio
      or:x:/../*/type?value
        =:select
      not
      add:x:/../*/return/**/input/*/.data-field/=options/.
        src
          visible:false
  else-if:x:/../*/type?value
    not
    add:x:/../*/return/**/input/*/.data-field/=options/.
      src
        visible:false
  if:x:/../*/show?value
    =:bool:false
    set:x:/../*/return/**/.data-field/=show-in-grid/./*/checked

  /*
   * If [name] is not given, we set focus to name widget.
   */
  if:x:/../*/name
    not
    add:x:/../*/return/**/.data-field/=field-name/.
      src
        oninit
          micro.page.set-focus:x:/../*/_event?value

  /*
   * Returning widget to caller.
   */
  eval-x:x:/+/*/*/*/*/*/*/*/*/value
  return
    container
      class:air-inner rounded shaded emphasized
      style:"padding-bottom:0 !important;margin-bottom:2rem;"
      widgets
        container
          class:row
          widgets
            container
              class:col strip fill
              widgets
                label
                  innerValue:Name
                input
                  type:text
                  placeholder:Name ...
                  title:Name of field
                  .data-field:field-name
                  value:x:/@.signal/--/name?value
                label
                  innerValue:Show
                span
                  widgets
                    input
                      type:checkbox
                      style:"margin-bottom:0;"
                      title:Show field in grid
                      .data-field:show-in-grid
                      checked
            container
              class:col strip fill
              widgets
                label
                  innerValue:Type
                  style:"margin-bottom:0;"
                select
                  .data-field:field-type
                  title:Type of field
                  onchange

                    /*
                     * Retrieving our "options" textbox.
                     */
                    p5.web.widgets.get-parent:x:/../*/_event?value
                    p5.web.widgets.find:x:/-/*/*?value
                      .data-field:options

                    /*
                     * Checking if we should enable our "options" textbox.
                     */
                    get-widget-property:x:/../*/_event?value
                      value
                    if:x:/@get-widget-property/*/*?value
                      =:select
                      or:x:/@get-widget-property/*/*?value
                        =:radio

                      /*
                       * Enabling our "options" textbox.
                       */
                      set-widget-property:x:/@p5.web.widgets.find/*/*?value
                        visible:true

                    else

                      /*
                       * Disabling our "options" textbox.
                       */
                      set-widget-property:x:/@p5.web.widgets.find/*/*?value
                        visible:false

                  widgets
                    option
                      innerValue:Singleline text
                      value:text
                    option
                      innerValue:Multiline text
                      value:textarea
                    option
                      innerValue:Checkbox
                      value:checkbox
                    option
                      innerValue:Number
                      value:number
                    option
                      innerValue:Select dropdown
                      value:select
                    option
                      innerValue:Radiobuttons
                      value:radio
                    option
                      innerValue:Date/time created
                      value:created
                input
                  type:text
                  placeholder:Options ...
                  title:Options, separated by comma
                  .data-field:options
                  value:x:/@.signal/--/options?value
                input
                  type:hidden
                  .data-field:.signal
                button
                  innerValue:@"<span class=""icon-bin""></span>"
                  title:Delete field
                  onclick

                    /*
                     * Retrieving parent widget, and simply deleting it.
                     */
                    p5.web.widgets.get-parent:x:/../*/_event?value
                    p5.web.widgets.get-parent:x:/-/*/*?value
                    delete-widget:x:/-/*/*?value
