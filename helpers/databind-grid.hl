/*
 * This file will databind the Camphora CRUD app manifest datagrid. Basically
 * listing all Hyperlambda files in user's CRUD app manifest folder, and allowing
 * the user to edit, create, and delete these, using a graphical UI.
 */





/*
 * Listing files in user's "apps folder", and creating one datagrid item for each
 * file that exists there.
 */
list-files:~/documents/private/camphora/
  filter:.hl
for-each:x:/@list-files/*?name

  /*
   * Figuring out filename, without extension.
   */
  split:x:/@_dp?value
    =:/
    =:.

  /*
   * Adding grid [item] to databind operation below.
   */
  eval-x:x:/+/*/*/*|/+/**/.filename
  add:x:/../*/micro.widgets.grid.databind
    src
      item
        .row
          style:"cursor:pointer;"
          onclick

            /*
             * Forward evaluated above.
             */
            .filename:x:/@_dp?value

            /*
             * Evaluating file responsible for editing app.
             */
            eval-x:x:/+/*
            micro.evaluate.file:@CAMPHORA/helpers/edit-app.hl
              filename:x:/@.filename?value
        name:x:/@split/0/-2?name
        edit
          widgets
            div
              class:strip right
              widgets

                /*
                 * Copy app manifest button.
                 */
                button
                  innerValue:@"<span class=""icon-loop2""></span>"
                  title:Create a copy of your app
                  onclick

                    /*
                     * Forward evaluated above.
                     */
                    .filename:x:/@_dp?value

                    /*
                     * Copying file, and re-databinding grid.
                     */
                    split:x:/@.filename?value
                      =:.
                    copy-file:x:/@.filename?value
                      dest:{0}-copy.hl
                        :x:/@split/0?name
                    micro.evaluate.file:@CAMPHORA/helpers/databind-grid.hl

                /*
                 * Download app manifest to client button.
                 */
                button
                  innerValue:@"<span class=""icon-download2""></span>"
                  title:Download app
                  onclick

                    /*
                     * Forward evaluated above.
                     */
                    .filename:x:/@_dp?value

                    /*
                     * Downloading Hyperlambda file wrapping app.
                     */
                    split:x:/@.filename?value
                      =:/
                    micro.download.file:x:/@.filename?value

                /*
                 * Delete app manifest button.
                 */
                button
                  innerValue:@"<span class=""icon-bin""></span>"
                  title:Delete your app entirely
                  onclick

                    /*
                     * Forward evaluated above.
                     */
                    .filename:x:/@_dp?value

                    /*
                     * Invoking file responsible for deleting app manifest.
                     */
                    eval-x:x:/+/*
                    micro.evaluate.file:@CAMPHORA/helpers/delete-app.hl
                      filename:x:/@.filename?value





/*
 * Databinding grid.
 */
micro.widgets.grid.databind:camphora-apps
