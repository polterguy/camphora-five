/*
 * File responsible for databinding Camphora apps datagrid.
 */





/*
 * Listing files in user's "apps folder", and creating one datagrid item for each
 * file that exists there.
 */
list-files:~/documents/private/camphora/
  filter:.hl
for-each:x:/@list-files/*?name

  /*
   * Figuring out filename, without extension.
   */
  split:x:/@_dp?value
    =:/
    =:.

  /*
   * Adding grid [item] to databind operation below.
   */
  eval-x:x:/+/*/*/*|/+/**/_filename
  add:x:/../*/micro.widgets.grid.databind
    src
      item
        name:x:/@split/0/-2?name
        edit
          widgets
            div
              class:strip right
              widgets

                /*
                 * Edit app manifest button.
                 */
                button
                  innerValue:@"<span class=""icon-pencil""></span>"
                  title:Edit your app
                  onclick

                    /*
                     * Forward evaluated above.
                     */
                    _filename:x:/@_dp?value

                    /*
                     * Evaluating file responsible for editing app.
                     */
                    eval-x:x:/+/*
                    micro.evaluate.file:@CAMPHORA/helpers/edit-app.hl
                      filename:x:/@_filename?value

                /*
                 * Copy app manifest button.
                 */
                button
                  innerValue:@"<span class=""icon-loop2""></span>"
                  title:Create a copy of your app
                  onclick

                    /*
                     * Forward evaluated above.
                     */
                    _filename:x:/@_dp?value

                    /*
                     * Copying file, and re-databinding grid.
                     */
                    split:x:/@_filename?value
                      =:.
                    copy-file:x:/@_filename?value
                      dest:{0}-copy.hl
                        :x:/@split/0?name
                    micro.evaluate.file:@CAMPHORA/helpers/databind-grid.hl

                /*
                 * Download app manifest to client button.
                 */
                button
                  innerValue:@"<span class=""icon-download2""></span>"
                  title:Download app
                  onclick

                    /*
                     * Forward evaluated above.
                     */
                    _filename:x:/@_dp?value

                    /*
                     * Downloading Hyperlambda file wrapping app.
                     */
                    split:x:/@_filename?value
                      =:/
                    eval-x:x:/+/*
                    micro.download.file:x:/@_filename?value
                      Content-Type:application/x-hyperlambda
                      Content-Disposition:@"attachment; filename=""{0}"""
                        :x:/@split/0/-?name

                /*
                 * Delete app manifest button.
                 */
                button
                  innerValue:@"<span class=""icon-bin""></span>"
                  title:Delete your app entirely
                  onclick

                    /*
                     * Forward evaluated above.
                     */
                    _filename:x:/@_dp?value

                    /*
                     * Invoking file responsible for deleting app manifest.
                     */
                    eval-x:x:/+/*
                    micro.evaluate.file:@CAMPHORA/helpers/delete-app.hl
                      filename:x:/@_filename?value





/*
 * Databinding grid.
 */
micro.widgets.grid.databind:camphora-apps
