
/*
 * File responsible for databinding Camphora apps datagrid.
 */





/*
 * Listing files in user's "apps folder", and creating one datagrid item for each
 * file that exists there.
 */
list-files:~/documents/private/camphora/
  filter:.hl
for-each:x:/@list-files/*?name

  /*
   * Figuring out filename, without extension.
   */
  split:x:/@_dp?value
    =:/
    =:.

  /*
   * Adding grid [item] to databind operation below.
   */
  eval-x:x:/+/*/*/*|/+/**/_filename
  add:x:/../*/micro.widgets.grid.databind
    src
      item
        name:x:/@split/0/-2?name
        edit
          class:right
          widgets
            div
              class:strip
              style:"display:inline-block;"
              widgets
                button
                  style:"margin-bottom:0;"
                  innerValue:@"<span class=""icon-pencil""></span>"
                  onclick

                    /*
                     * Forward evaluated above.
                     */
                    _filename:x:/@_dp?value

                    /*
                     * Evaluating file responsible for editing app.
                     */
                    eval-x:x:/+/*
                    micro.evaluate.file:@CAMPHORA/helpers/edit-app.hl
                      filename:x:/@_filename?value

                button
                  style:"margin-bottom:0;"
                  innerValue:@"<span class=""icon-bin""></span>"
                  onclick

                    /*
                     * Deleting app, after having asked user to confirm action, for then
                     * to simply re-databind grid.
                     */
                    create-widgets
                      micro.widgets.modal:camphora-confirm-deletion
                        widgets
                          h3
                            innerValue:Confirm deletion of app
                          p
                            innerValue:Are you sure you want to delete this app? This action is permanent, and your application will also be uninstalled.
                          label
                            widgets
                              input:delete-data
                                type:checkbox
                              span
                                innerValue:Also delete the data for app
                          div
                            class:right
                            widgets
                              div
                                class:strip
                                style:"display:inline-block"
                                widgets
                                  button
                                    innerValue:Yes
                                    style:"margin-bottom:0;"
                                    oninit

                                      /*
                                       * Setting initial focus to "Yes" button.
                                       */
                                      micro.page.set-focus:x:/../*/_event?value

                                    onclick

                                      /*
                                       * Forward evaluated above.
                                       */
                                      _filename:x:/@_dp?value

                                      /*
                                       * Making sure we also uninstall app.
                                       */
                                      split:x:/@_filename?value
                                        =:/
                                        =:.
                                      desktop.uninstall-app:/modules/{0}/
                                        :x:/@split/0/-2?name

                                      /*
                                       * Then checking if we should delete the database table for our app.
                                       */
                                      get-widget-property:delete-data
                                        checked
                                      if:x:/@get-widget-property/*/*

                                        /*
                                         * Opening up database connection to camphora, and deleting the table, making
                                         * sure we wrap it in a try/catch block, in case it doesn't exist, or some other 
                                         * error occurs.
                                         */
                                        p5.mysql.connect:[camphora]
                                          p5.mysql.execute:@"drop table `{0}`"
                                            :x:/@split/0/-2?name

                                      /*
                                       * Deleting app declaration file, for then to simply
                                       * delete modal widget.
                                       */
                                      delete-file:x:/@_filename?value
                                      delete-widget:camphora-confirm-deletion

                                      /*
                                       * Re-databinding grid.
                                       */
                                      micro.evaluate.file:@CAMPHORA/helpers/databind-grid.hl

                                      /*
                                       * Signaling listeners that app's been deleted.
                                       */
                                      camphora.delete-app:x:/@_filename?value

                                  button
                                    innerValue:No
                                    style:"margin-bottom:0;"
                                    onclick

                                      /*
                                       * Simply deleting modal widget.
                                       */
                                      delete-widget:camphora-confirm-deletion





/*
 * Databinding grid.
 */
micro.widgets.grid.databind:camphora-apps
