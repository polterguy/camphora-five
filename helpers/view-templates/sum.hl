/*
 * A "view sum" type of view template for Camphora Five.
 *
 * Will sum all [field]'s for your database by the value of another
 * [field].
 */





/*
 * Contains the parts of this view that needs to be configured.
 */
.configure
  .description:@"This creates a chart view, that will sum all values for some field, grouped
by the value of another field, resulting in a visual chart, displaying the relative sums
for you. This view is useful for things such as displaying total sales for employees, etc.

__Notice__, this view expects you to at least have one _'number'_ type of field, that you
want to sum up - In addition to another field in your app to group your sums by. This view
will not function correctly if you have too many distinct values for your _'Group by'_ field.

* __[Group by]__ - What field to group your data by.
* __[Sum field]__ - What field to sum.
* __[Chart header]__ - Descriptive header for your chart.
* __[Chart type]__ - What type of chart to use. You can use a pie chart or a bar chart.
* __[Chart description]__ - A description of your chart, feel free to use Markdown syntax here."
  image:"https://phosphorusfive.files.wordpress.com/2018/05/screenshot-count-view.png"
  .condition

    /*
     * Verifies that app manifest contains at least one 'number' type of field,
     * in addition to at least one type of 'select' or 'radio' type of field.
     */
    p5.web.widgets.find-first
      .fields
    micro.form.serialize:x:/-/*/*?value
    if:x:/@micro.form.serialize/*/field-type/=number
      and:x:/@micro.form.serialize/*/field-type(/=select|/=radio|/=user)
      return:bool:true
    return:bool:false

  field:Group by
    select
    radio
    user
  field:Sum field
    number
  text:Chart header
  radio:Chart type
    option:Bar chart
      onchange
        get-widget-property:x:/../*/_event?value
          checked
        if:x:/-/*/*
          camphora._set-view-screenshot:"https://phosphorusfive.files.wordpress.com/2018/05/screenshot-count-view.png"
    option:Pie chart
      onchange
        get-widget-property:x:/../*/_event?value
          checked
        if:x:/-/*/*
          camphora._set-view-screenshot:"https://phosphorusfive.files.wordpress.com/2018/05/screenshot-pie-chart-sales-2.png"
  markdown:Chart description





/*
 * Contains the field name to group your SQL by.
 */
.group-by-field-name:{Group by}





/*
 * Contains the field to aggregate the sum by.
 */
.sum-field-name:{Sum field}





/*
 * Contains the chart type.
 */
.chart-type:{Chart type}





/*
 * Contains the chart header.
 */
.chart-header:{Chart header}





/*
 * Contains the chart description.
 */
.chart-description:{Chart description}





/*
 * Includes Micro's CSS files.
 */
micro.css.include





/*
 * Then we connect to our database, and select our sum aggregate values.
 */
p5.mysql.connect:[camphora]

  /*
   * Selecting Markdown field from database.
   */
  p5.mysql.select:select `{2}`, sum(`{0}`) from `{1}` group by `{2}`
    :x:/@.sum-field-name?value
    :{app-name}
    :x:/@.group-by-field-name?value

  /*
   * Adding our data to our bar chart below.
   */
  for-each:x:/@p5.mysql.select/*
    set:x:/+2/*/*?name
      src:x:/@_dp/#/0?value
    eval-x:x:/+/*/*
    add:x:/../*/create-widget/**/micro.widgets.chart.column/*/data
      src
        foo:x:/@_dp/#/1?value





/*
 * Checking what type of chart we should create.
 */
if:x:/@.chart-type?value
  =:Pie chart
  set:x:/../*/create-widget/**/micro.widgets.chart.column?name
    src:micro.widgets.chart.pie





/*
 * Making sure we turn the chart's description into HTML, assuming
 * it is Markdown.
 */
micro.markdown.markdown2html:x:/../*/.chart-description?value





/*
 * Creating our main wire frame widget.
 */
create-widget
  class:container
  widgets
    div
      class:row
      widgets
        div
          class:col
          widgets
            h3
              class:center
              innerValue:x:/@.chart-header?value
    div
      class:row
      widgets
        div
          class:col-50
          innerValue:x:/@micro.markdown.markdown2html?value
        div
          class:col-50
          widgets
            micro.widgets.chart.column
              data
