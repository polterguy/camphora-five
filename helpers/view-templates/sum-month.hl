/*
 * A "view sum by month" type of view template for Camphora Five.
 *
 * Will sum all [field]'s for your database by the value of another
 * [field].
 */





/*
 * Contains the parts of this view that needs to be configured.
 */
.configure
  .description:@"This creates a chart view, that will sum all values for some _'number'_ field,
grouped by the month of another _'created'_ type of field, resulting in a visual chart, displaying
the sums for you on a month to month basis. This view is useful for things such as displaying
sales per month, etc. The bar chart supports negative values.

__Notice__, this view expects you to at least have one _'number'_ type of field,
in addition to another _'created'_ type field in your app to group your sums by.

* __[Group by]__ - What 'created' field to group your data by.
* __[Sum field]__ - What 'number' field to sum.
* __[Chart header]__ - Descriptive header for your chart.
* __[Chart description]__ - A description of your chart, feel free to use Markdown syntax here."
  image:"https://phosphorusfive.files.wordpress.com/2018/05/screenshot-sum-chart.png"
  .condition

    /*
     * Verifies that app manifest contains at least one 'number' type of field,
     * in addition to at least one type of 'select' or 'radio' type of field.
     */
    p5.web.widgets.find-first
      .fields
    micro.form.serialize:x:/-/*/*?value
    if:x:/@micro.form.serialize/*/field-type/=number
      and:x:/@micro.form.serialize/*/field-type/=created
      return:bool:true
    return:bool:false

  field:Group by
    created
  field:Sum field
    number
  text:Chart header
  markdown:Chart description





/*
 * Contains the field name to group your SQL by.
 */
.group-by-field-name:{Group by}





/*
 * Contains the field to aggregate the sum by.
 */
.sum-field-name:{Sum field}





/*
 * Contains the chart header.
 */
.chart-header:{Chart header}





/*
 * Contains the chart description.
 */
.chart-description:{Chart description}





/*
 * Includes Micro's CSS files.
 */
micro.css.include





/*
 * Then we connect to our database, and select our sum aggregate values.
 */
p5.mysql.connect:[camphora]

  /*
   * Selecting Markdown field from database.
   */
  p5.mysql.select:select DATE_FORMAT(`{2}`,'%Y, %b') as Date, sum(`{0}`) from `{1}` group by Date
    :x:/@.sum-field-name?value
    :{app-name}
    :x:/@.group-by-field-name?value

  /*
   * Adding our data to our bar chart below.
   */
  for-each:x:/@p5.mysql.select/*
    set:x:/+2/*/*?name
      src:x:/@_dp/#/0?value
    eval-x:x:/+/*/*
    add:x:/../*/create-widget/**/micro.widgets.chart.column/*/data
      src
        foo:x:/@_dp/#/1?value





/*
 * Making sure we turn the chart's description into HTML, assuming
 * it is Markdown.
 */
markdown2html:x:/../*/.chart-description?value





/*
 * Creating our main wire frame widget.
 */
create-widget
  class:container
  widgets
    div
      class:row
      widgets
        div
          class:col
          widgets
            h3
              class:center
              innerValue:x:/@.chart-header?value
    div
      class:row
      widgets
        div
          class:col-50
          innerValue:x:/@markdown2html?value
        div
          class:col-50
          widgets
            micro.widgets.chart.column
              data
