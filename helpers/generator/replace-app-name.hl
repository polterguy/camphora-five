/*
 * This file will recursively load all Hyperlambda files from the
 * specified __[folder]__, and replace all occurrencies of '{app-name}'
 * with the given __[app-name]__ value.
 */





/*
 * Sanity checking invocation.
 */
micro.lambda.contract.min:x:/..
  folder:string
  app-name:string





/*
 * Active Events that needs to have their names changed.
 */
eval-x:x:/+/*/*
.events
  camphora.version
    :camphora.{0}.version
      :x:/../*/app-name?value
  camphora.widgets.datagrid
    :camphora.{0}.widgets.datagrid
      :x:/../*/app-name?value
  camphora.widgets.toolbar
    :camphora.{0}.widgets.toolbar
      :x:/../*/app-name?value
  camphora.create-new-item
    :camphora.{0}.create-new-item
      :x:/../*/app-name?value
  camphora.download-items
    :camphora.{0}.download-items
      :x:/../*/app-name?value
  camphora.datagrid.sort
    :camphora.{0}.datagrid.sort
      :x:/../*/app-name?value
  camphora.datagrid.search
    :camphora.{0}.datagrid.search
      :x:/../*/app-name?value
  camphora.datagrid.page.previous
    :camphora.{0}.datagrid.page.previous
      :x:/../*/app-name?value
  camphora.datagrid.databind.next
    :camphora.{0}.datagrid.databind.next
      :x:/../*/app-name?value
  camphora.datagrid.databind
    :camphora.{0}.datagrid.databind
      :x:/../*/app-name?value
  camphora.database.import-csv-file
    :camphora.{0}.database.import-csv-file
      :x:/../*/app-name?value
  camphora.database.delete-all-filtered-items
    :camphora.{0}.database.delete-all-filtered-items
      :x:/../*/app-name?value
  camphora.database.update
    :camphora.{0}.database.update
      :x:/../*/app-name?value
  camphora.database.insert
    :camphora.{0}.database.insert
      :x:/../*/app-name?value
  camphora.datagrid.page
    :camphora.{0}.datagrid.page
      :x:/../*/app-name?value
  camphora.datagrid.order-by
    :camphora.{0}.datagrid.order-by
      :x:/../*/app-name?value
  camphora.datagrid.order-dir
    :camphora.{0}.datagrid.order-dir
      :x:/../*/app-name?value
  camphora.datagrid.filter
    :camphora.{0}.datagrid.filter
      :x:/../*/app-name?value
  camphora.datagrid.databound
    :camphora.{0}.datagrid.databound
      :x:/../*/app-name?value
  camphora.datagrid.row-clicked
    :camphora.{0}.datagrid.row-clicked
      :x:/../*/app-name?value
  camphora.datagrid.edited-item-id
    :camphora.{0}.datagrid.edited-item-id
      :x:/../*/app-name?value
  desktop.help-files.generated-app
    :desktop.help-files.{0}.generated-app
      :x:/../*/app-name?value




/*
 * Loading all Hyperlambda files from specified [folder].
 */
list-files:x:/../*/folder?value
  filter:.hl
load-file:x:/-/*?name
  convert:false
for-each:x:/-/*

  /*
   * Loading currently iterated file.
   */
  hyper2lambda:x:/@_dp/#?value
    keep-comments:true

  /*
   * Replacing {app-name} with [app-name]'s value.
   */
  set:x:/@hyper2lambda/**/={app-name}?value
    src:x:/../*/app-name?value

  /*
   * Iterating through each event in our above [.events] lambda,
   * and making sure all values and names from all nodes in currently
   * iterated file that contains the value/name of the currently iterated
   * event name is exchanged with its actual name/value.
   */
  for-each:x:/../*/.events/*

    /*
     * First changing node names.
     */
    set:x:/@hyper2lambda/**/{0}?name
      :x:/@_dp/#?name
      src:x:/@_dp/#/*?value

    /*
     * Then exchanging node values.
     */
    set:x:/@hyper2lambda/**/={0}?value
      :x:/@_dp/#?name
      src:x:/@_dp/#/*?value

  /*
   * Converting file to Hyperlambda, making sure we keep its comments,
   * and saves it again.
   */
  lambda2hyper:x:/@hyper2lambda/*
    comments:unroll
  save-file:x:/@_dp/#?name
    src:x:/@lambda2hyper?value





/*
 * Recursively invoking self for each folder inside of [folder].
 */
list-folders:x:/../*/folder?value
for-each:x:/-/*?name
  eval-x:x:/+/*
  micro.evaluate.file:@CAMPHORA/helpers/generator/replace-app-name.hl
    folder:x:/@_dp?value
    app-name:x:/../*/app-name?value
