
/*
 * File responsible for editing and creating new apps.
 *
 * Optionally takes a [filename] argument, for editing existing app, instead of
 * creating a new app.
 */





/*
 * Sanity check.
 */
micro.lambda.contract.optional:x:/..
  filename:string





/*
 * Default values.
 */
.defaults
  name:new-app
  filename





/*
 * Used to hold attributes of app retrieved from app-declaration file, if any.
 */
.file-values
  viewers
  editors
  skin
  show-headers





/*
 * Modifying defaults, if a [filename] argument was supplied.
 */
if:x:/../*/filename?value

  /*
   * We're in "edit modus", hence we change the [.defaults] segment to contain
   * data from our actual file.
   */
  split:x:/../*/filename?value
    =:/
    =:.
  set:x:/@.defaults/*/name?value
    src:x:/@split/0/-2?name
  set:x:/@.defaults/*/filename?value
    src:x:/../*/filename?value

  /*
   * Loading file, and making sure initial state of widget reflects file's actual
   * declaration.
   */
  load-file:x:/../*/filename?value

  /*
   * Applying settings for later references.
   */
  set:x:/@.file-values/*/viewers?value
    src:x:/@load-file/*/*/viewers?value
  set:x:/@.file-values/*/editors?value
    src:x:/@load-file/*/*/editors?value
  set:x:/@.file-values/*/skin?value
    src:x:/@load-file/*/*/skin?value
  set:x:/@.file-values/*/show-headers?value
    src:x:/@load-file/*/*/show-headers?value

  /*
   * Looping through each [field] from file, and creating a wrapper widget for it.
   */
  for-each:x:/@load-file/*/*/field

    /*
     * Adding a widget into fields wrapper widget, reflecting currently iterated field declaration.
     */
    join:x:/@_dp/#/*/type/*?name
      sep:", "
    eval-x:x:/+/*/*/*
    add:x:/../*/create-widget/**/div/*/.fields/./*/widgets
      src
        camphora.widgets._internals.field-definition
          name:x:/@_dp/#/*/name?value
          type:x:/@_dp/#/*/type?value
          options:x:/@join?value
          show:x:/@_dp/#/*/show?value

else

  /*
   * "New app modus".
   */
  whoami
  set:x:/@.defaults/*/filename?value
    src:/users/{0}/documents/private/camphora/{1}.hl
      :x:/@whoami/*/username?value
      :x:/@.defaults/*/name?value





/*
 * Checking if app is already being edited, and if so, simply scrolling existing
 * editor into view.
 */
p5.web.widgets.find
  .filename:x:/@.defaults/*/filename?value
if:x:/@p5.web.widgets.find/*/*?value

  /*
   * App is already being edited, scrolling existing editor into view, and returning early.
   */
  p5.web.send-javascript:@"p5.$('{0}').el.scrollIntoView(true);"
    :x:/@p5.web.widgets.find/*/*?value
  p5.web.widgets.find:x:/@p5.web.widgets.find/*/*?value
    .data-field:name
  micro.page.set-focus:x:/@p5.web.widgets.find/*/*?value
  return





/*
 * Creating main wire frame for editing app.
 */
eval-x:x:/+/*/.filename|/+/**/micro.widgets.wizard-form/*/*/value|/+/**(/.file-values/*)
create-widget
  class:row
  .filename:x:/@.defaults/*/filename?value
  events

    /*
     * Invoked when an app is deleted.
     *
     * We're checking if the deleted app, is the currently edited one, and if so,
     * we delete the widget.
     */
    camphora.delete-app

      /*
       * Sanity check.
       */
      micro.lambda.contract.min:x:/..
        _arg:string

      /*
       * Checking if this is our guy, at which point we delete widget entirely.
       */
      get-widget-property:x:/../*/_event?value
        .filename
      if:x:/@get-widget-property/*/*?value
        =:x:/../*/_arg?value

        /*
         * Our guy, making sure we delete widget, to not have it "dangling".
         */
        delete-widget:x:/../*/_event?value

  widgets
    div
      class:col
      widgets
        div
          class:shaded rounded air-inner
          .data
          widgets
            h4
              innerValue:Meta info
            micro.widgets.wizard-form
              collection
                class:row
                widgets

                  /*
                   * Name of app.
                   */
                  collection
                    class:col-33
                    widgets
                      text
                        info:Name
                        .data-field:name
                        value:x:/@.defaults/*/name?value
                        title:Name of your app. Notice, this also declares the name of your database.
                        oninit

                          /*
                           * Setting initial focus to name textbox.
                           */
                          micro.page.set-focus:x:/../*/_event?value

                  /*
                   * Role that is allowed to see the data.
                   */
                  collection
                    class:col-33
                    widgets
                      select
                        .data-field:viewers
                        info:Viewers
                        title:Minimum role that is allowed to see data in your app. Guest means also people who are not logged into the system.
                        oninit

                          /*
                           * Forward evaluated before creation of widget.
                           */
                          .file-values
                            viewers:x:/../*/.file-values/*/viewers?value

                          /*
                           * Creating one option element for each role in system.
                           */
                          p5.auth.roles.list
                          for-each:x:/-/*?name

                            /*
                             * Checking if this is our currently selected value.
                             */
                            if:x:/@.file-values/*/viewers?value
                              =:x:/@_dp?value

                              /*
                               * Yup, currently iterated value should be our selected guy.
                               */
                              add:x:/..for-each/*/create-widget
                                src:selected

                            /*
                             * Creating option element.
                             */
                            create-widget
                              element:option
                              parent:x:/../*/_event?value
                              innerValue:x:/@_dp?value

                  /*
                   * Role that is allowed to edit the data.
                   */
                  collection
                    class:col-33
                    widgets
                      select
                        .data-field:editors
                        info:Editors
                        title:Minimum role that is allowed to edit data in your app. Guest means also people who are not logged into the system.
                        oninit

                          /*
                           * Forward evaluated before creation of widget.
                           */
                          .file-values
                            editors:x:/../*/.file-values/*/editors?value

                          /*
                           * Creating one option element for each role in system.
                           */
                          p5.auth.roles.list
                          for-each:x:/-/*?name

                            /*
                             * Checking if this is our currently selected value.
                             */
                            if:x:/@.file-values/*/editors?value
                              =:x:/@_dp?value

                              /*
                               * Yup, currently iterated value should be our selected guy.
                               */
                              add:x:/..for-each/*/create-widget
                                src:selected

                            /*
                             * Checking if there are no currently selected value from file,
                             * at which point we default our value to a sane default.
                             */
                            if:x:/@.file-values/*/editors?value
                              not
                              and:x:/@_dp?value
                                =:root

                              /*
                               * This guy should be selected by default.
                               */
                              add:x:/..for-each/*/create-widget
                                src:selected

                            /*
                             * Creating option element.
                             */
                            create-widget
                              element:option
                              parent:x:/../*/_event?value
                              innerValue:x:/@_dp?value

            hr
            div
              class:row
              widgets

                /*
                 * Skin select drop down list wrapper.
                 */
                div
                  class:col-100
                  widgets
                    h4
                      innerValue:Styling
                div
                  class:col-33
                  widgets
                    div
                      class:strip fill
                      widgets
                        label
                          for:camphora-select-skin
                          innerValue:Skin
                        container:camphora-select-skin
                          element:select
                          .data-field:skin
                          oninit

                            /*
                             * Forward evaluated before creation of widget.
                             */
                            .file-values
                              skin:x:/../*/.file-values/*/skin?value

                            /*
                             * Checking which Micro skins exists in installation.
                             */
                            list-files:@MICRO/media/skins/
                              filter:.css

                            /*
                             * Creating one option element for each skin.
                             */
                            for-each:x:/@list-files/*?name

                              /*
                               * Splitting currently iterated file, to figure out
                               * only its name.
                               */
                              split:x:/@_dp?value
                                =:/
                                =:.

                              /*
                               * Checking if this is our currently selected value.
                               */
                              if:x:/@.file-values/*/skin?value
                                =:x:/@split/0/-2?name

                                /*
                                 * Yup, currently iterated value should be our selected guy.
                                 */
                                add:x:/..for-each/*/create-literal-widget
                                  src:selected

                              /*
                               * Creating option element.
                               */
                              create-literal-widget
                                parent:x:/../*/_event?value
                                element:option
                                innerValue:x:/@split/0/-2?name

                /*
                 * Show headers checkbox.
                 */
                div
                  class:col-33
                  widgets
                    input:camphora-show-headers
                      type:checkbox
                      .data-field:show-headers
                      style:"margin-top:6px;margin-bottom:10px;"
                      oninit

                        /*
                         * Forward evaluated before creation of widget.
                         */
                        .file-values
                          show-headers:x:/../*/.file-values/*/show-headers?value
                        if:x:/@.file-values/*/show-headers?value

                          /*
                           * Widget should be initially checked.
                           */
                          set-widget-property:x:/../*/_event?value
                            checked

                    label
                      for:camphora-show-headers
                      innerValue:Show headers
                      style:"margin-top:6px;margin-bottom:10px;display:inline-block;"

            hr
            div
              .fields
              widgets
                h4
                  innerValue:Fields

            div
              class:right
              widgets
                div
                  class:strip
                  style:"display:inline-block;"
                  widgets
                    button
                      style:"margin-bottom:0;"
                      innerValue:@"<span class=""icon-plus""></span>"
                      title:Adds another field to your CRUD form
                      onclick

                        /*
                         * Adding a new field into form.
                         */
                        p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                          .filename
                        p5.web.widgets.find:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                          .fields

                        /*
                         * Creating widget wrapping field definition.
                         */
                        eval-x:x:/+/*/*
                        create-widgets
                          camphora.widgets._internals.field-definition
                            parent:x:/@p5.web.widgets.find/*/*?value

                    button
                      style:"margin-bottom:0;"
                      innerValue:Save
                      .save-button
                      onclick

                        /*
                         * Invoking event responsible for saving app.
                         */
                        camphora.save-app:x:/../*/_event?value

                      events

                        /*
                         * Event responsible for saving our app.
                         */
                        camphora.save-app

                          /*
                           * Checking if this is our guy.
                           */
                          if:x:/../*/_event?value
                            !=:x:/../*/_arg?value

                            /*
                             * Not our guy, returning early.
                             */
                            return

                          /*
                           * Retrieving wizard-form's data.
                           */
                          p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                            .data
                          micro.form.serialize:x:/@p5.web.widgets.find-first-ancestor/*/*?value

                          /*
                           * Sanity checking name of app.
                           */
                          match:x:/@micro.form.serialize/*/name?value
                            src:regex:@"/^[-a-z]{3,}$/"
                          if:x:/@match/0?name
                            =:
                            or:x:/@match/*?count
                              =:int:0
                            or
                              fetch:x:/0/0?value
                                file-exists:~/documents/private/camphora/{0}.hl
                                  :x:/@micro.form.serialize/*/name?value
                              and
                                fetch:x:/*/.different?value
                                  p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                                    .filename
                                  get-widget-property:x:/-/*/*?value
                                    .filename
                                  .different:bool:false
                                  whoami
                                  if:x:/@get-widget-property/*/*?value
                                    !=:/users/{0}/documents/private/camphora/{1}.hl
                                      :x:/@whoami/*/username?value
                                      :x:/@micro.form.serialize/*/name?value
                                    set:x:/@.different?value
                                      src:bool:true

                            /*
                             * App's name is not accepted.
                             */
                            micro.windows.info:Your app can only have a-z and hyphens (-) in your app's name, and the name must be 3 characters or longer. It must also not be an existing module, and it must be unique.
                              class:micro-windows-info warning
                            p5.web.widgets.find:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                              .data-field:name
                            micro.page.set-focus:x:/@p5.web.widgets.find/*/*?value
                            return:bool:false

                          /*
                           * Making sure app has at least one column.
                           */
                          if:x:/@micro.form.serialize/*/.signal?count
                            =:int:0

                            /*
                             * Oops, app has no field declarations.
                             */
                            micro.windows.info:Your app must have at least one field
                              class:micro-windows-info warning
                            return:bool:false

                          /*
                           * Retrieving app's filename, and if it is not null, we delete
                           * the file.
                           */
                          p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                            .filename
                          get-widget-property:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                            .filename
                          if:x:/@get-widget-property/*/*?value
                            !=

                            /*
                             * Editing mode, making sure we delete existing file,
                             * if it exists.
                             */
                            if
                              fetch:x:/0/0?value
                                file-exists:x:/@get-widget-property/*/*?value

                              /*
                               * File exists, deleting it.
                               */
                              delete-file:x:/@get-widget-property/*/*?value

                          /*
                           * Used to hold actual data for our app.
                           */
                          .data

                          /*
                           * Iterating through each field, and adding as field into
                           * above [.data] lambda.
                           */
                          for-each:x:/@micro.form.serialize/*/.signal

                            /*
                             * Sanity checking field-name before we add it into resulting lambda.
                             */
                            match:x:/@_dp/#/@field-name?value
                              src:regex:@"/^[-a-z]{3,}$/"
                            if:x:/@match/*?count
                              =:int:0
                              or:x:/@match/0?name
                                =:

                              /*
                               * Oops, not an accepted name!
                               */
                              micro.windows.info:The names of your fields must only contain lowercase letters [a-z], hyphens (-), and be minimum 3 characters long
                                class:micro-windows-info warning
                              return:bool:false

                            eval-x:x:/+/*/*/*
                            add:x:/@.data
                              src
                                field
                                  name:x:/@_dp/#/@field-name?value
                                  type:x:/@_dp/#/@field-type?value
                                  show:x:/@_dp/#/@show-in-grid?value
                            if:x:/@_dp/#/@options?value

                              /*
                               * Splitting up options semantically for each "," found
                               * in its value.
                               */
                              split:x:/@_dp/#/@options?value
                                =:,
                                trim:true
                              for-each:x:/@split/*?name
                                add:x:/@.data/0/-/*/type
                                  src:x:/@_dp?value

                          /*
                           * Adding other types of settings for app.
                           */
                          add:x:/../*/.data
                            src:x:/@micro.form.serialize/*(/viewers|/editors|/skin|/show-headers)

                          /*
                           * Saving file.
                           */
                          lambda2hyper:x:/@.data/*
                          save-file:~/documents/private/camphora/{0}.hl
                            :x:/@micro.form.serialize/*/name?value
                            src:x:/@lambda2hyper?value

                          /*
                           * Updating header.
                           */
                          p5.web.widgets.find:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                            element:h3
                          set-widget-property:x:/@p5.web.widgets.find/*/*?value
                            innerValue:x:/@micro.form.serialize/*/name?value

                          /*
                           * Updating [.filename] to signal we're in "edit mode".
                           */
                          whoami
                          set-widget-property:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                            .filename:/users/{0}/documents/private/camphora/{1}.hl
                              :x:/@whoami/*/username?value
                              :x:/@micro.form.serialize/*/name?value

                          /*
                           * Re-databinding grid.
                           */
                          micro.evaluate.file:@CAMPHORA/helpers/databind-grid.hl

                          /*
                           * Notifying user.
                           */
                          micro.windows.info:App was saved
                            class:micro-windows-info success

                    button
                      style:"margin-bottom:0;"
                      innerValue:Generate
                      onclick

                        /*
                         * Asking user to confirm action.
                         */
                        eval-x:x:/+/**/_widget
                        create-widgets
                          micro.widgets.modal:camphora-confirm-app-creation
                            widgets
                              h3
                                innerValue:Please confirm action
                              p
                                innerValue:@"This will delete any existing apps with the same name, and also delete any existing data for these apps, if you choose to check off the <em>'delete data'</em> checkbox. Are you sure you wish to proceed?"
                              p
                                innerValue:@"<strong>Notice</strong>, - If you have changed any of your fields to become different types, you might have to delete your data in order to have your app working. If you get weird errors as you run your app, try to re-generate your app, and delete your data as you do."
                              input:camphora-delete-data
                                type:checkbox
                                style:"margin-bottom:0;"
                              label
                                for:camphora-delete-data
                                innerValue:Delete existing data
                              div
                                class:right
                                widgets
                                  div
                                    class:strip
                                    style:"display:inline-block;"
                                    widgets
                                      button
                                        innerValue:Yes
                                        style:"margin-bottom:0;"
                                        oninit

                                          /*
                                           * Setting focus initially to "Yes" button.
                                           */
                                          micro.page.set-focus:x:/../*/_event?value

                                        onclick

                                          /*
                                           * Forward evaluated above.
                                           */
                                          _widget:x:/../*/_event?value

                                          /*
                                           * Finding parent widget, such that we can find
                                           * save button.
                                           */
                                          p5.web.widgets.get-parent:x:/../*/_widget?value
                                          p5.web.widgets.find:x:/-/*/*?value
                                            .save-button

                                          /*
                                           * Invoking event responsible for saving app, and
                                           * verifying that saving operation was successful
                                           * before we proceed to generating our app.
                                           */
                                          camphora.save-app:x:/@p5.web.widgets.find/*/*?value
                                          if:x:/@camphora.save-app?value
                                            =:bool:false

                                            /*
                                             * Saving of form was not successful.
                                             * Returning early, to abort generating of app.
                                             */
                                            return

                                          /*
                                           * Checking if user wants to delete any existing data for app.
                                           */
                                          get-widget-property:camphora-delete-data
                                            checked
                                          if:x:/-/*/*
                                            add:x:/../**/micro.evaluate.file
                                              src
                                                delete-data:bool:true

                                          /*
                                           * Retrieving filename widget, and invoking 
                                           * file responsible for generating our app.
                                           */
                                          p5.web.widgets.find-first-ancestor:x:/../*/_widget?value
                                            .filename
                                          get-widget-property:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                                            .filename

                                          /*
                                           * Invoking file responsible for generating our app, making
                                           * sure we wrap the invocation in a try/catch block, in case
                                           * exceptions occurs.
                                           */
                                          try

                                            /*
                                             * Evaluating file responsible for creating our app.
                                             */
                                            eval-x:x:/+/*
                                            micro.evaluate.file:@CAMPHORA/helpers/generate-app.hl
                                              filename:x:/@get-widget-property/*/*?value

                                            /*
                                             * Notifying user.
                                             */
                                            micro.windows.info:App was successfully generated
                                              class:micro-windows-info success

                                            /*
                                             * Deleting modal widget.
                                             */
                                            delete-widget:camphora-confirm-app-creation

                                          catch

                                            /*
                                             * Oops ...!!
                                             */
                                            micro.windows.info:x:/@message?value
                                              class:micro-windows-info warning

                                      button
                                        innerValue:No
                                        style:"margin-bottom:0;"
                                        onclick

                                          /*
                                           * Deleting modal widget.
                                           */
                                          delete-widget:camphora-confirm-app-creation

                    button
                      style:"margin-bottom:0;"
                      innerValue:Close
                      onclick

                        /*
                         * Finding main "editing" widget, and deleting it.
                         */
                        p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                          .filename
                        delete-widget:x:/-/*/*?value
