/*
 * File responsible for generating a Camphora Five CRUD app from a manifest file.
 *
 * Expects a __[filename]__ argument, declaring which app to generate.
 * Optionally pass in a __[delete-data]__ argument, and set its value to boolean _"true"_
 * if you wish to delete existing data/database/table for app.
 */





/*
 * Sanity check.
 */
micro.lambda.contract.min:x:/..
  filename:string





/*
 * Figuring out app's folder name.
 */
split:x:/../*/filename?value
  =:/
  =:.





/*
 * Checking if folder exists from before, and if it does, we delete it.
 */
if
  fetch:x:/0/0?value
    folder-exists:/modules/{0}/
      :x:/@split/0/-2?name
  delete-folder:/modules/{0}/
    :x:/@split/0/-2?name





/*
 * Creating folder structure for our app.
 */
create-folder:/modules/{0}/
  :x:/@split/0/-2?name





/*
 * Copying all files and folders from "template" folder into app's folder.
 */
list-files:@CAMPHORA/template/
copy-file:x:/@list-files/*?name
  dest:/modules/{0}/
    :x:/@split/0/-2?name
list-folders:@CAMPHORA/template/
for-each:x:/-/*?name
  split:x:/@_dp?value
    =:/
  copy-folder:x:/@_dp?value
    dest:/modules/{0}/{1}/
      :x:/..for-each/@split/0/-2?name
      :x:/@split/0/-?name





/*
 * Checking if manifest for application specified that we should create a
 * desktop icon, or not
 */
load-file:x:/../*/filename?value
if:x:/@load-file/*/*/desktop-icon?value
  =:bool:false

  /*
   * Deleting desktop icon for app, since it obviously should not have one.
   */
  delete-file:/modules/{0}/desktop.hl
    :x:/@split/0/-2?name

else

  /*
   * Loading and modifying app's "desktop.hl" file, to make sure it displays
   * what app this actually is.
   *
   * Making sure we keep comments in file.
   */
  load-file:/modules/{0}/desktop.hl
    :x:/@split/0/-2?name
    convert:false
  hyper2lambda:x:/@load-file/*?value
    keep-comments:true
  eval-x:x:/+/*/*
  add:x:/@hyper2lambda/*/*/widgets/*/literal/[0,1]
    src
      innerValue:x:/@split/0/-2?name
  lambda2hyper:x:/@hyper2lambda/*
    comments:unroll
  save-file:/modules/{0}/desktop.hl
    :x:/@split/0/-2?name
    src:x:/@lambda2hyper?value





/*
 * Copying file that declares our app into folder for app, for then to load the
 * file, and removing all [views] from it, to create a separate Hyperlambda file,
 * encapsulating each view in its own separate Hyperlambda file.
 */
copy-file:x:/../*/filename?value
  dest:/modules/{0}/helpers/app-manifest.hl
    :x:/@split/0/-2?name
load-file:/modules/{0}/helpers/app-manifest.hl
  :x:/@split/0/-2?name
for-each:x:/@load-file/*/*/views/*
  save-file:/modules/{0}/views/{1}.hl
    :x:/@split/0/-2?name
    :x:/@_dp/#?name
    src:{0}{1}
      :@"/*
 * This file was auto-generated by Camphora Five.
 *
 * Edit it at your own risk, since it might be overwritten
 * if the app is re-generated!
 */

"
      :x:/@_dp/#?value
set:x:/@load-file/*/*/views/*?value
lambda2hyper:x:/@load-file/*/*
save-file:/modules/{0}/helpers/app-manifest.hl
  :x:/@split/0/-2?name
  src:x:/@lambda2hyper?value





/*
 * Trying to delete old database table, if it exists, and if caller told us to do so.
 */
if:x:/../*/delete-data?value
  try
    p5.mysql.connect:[camphora]
      p5.mysql.execute:@"drop table `{0}`"
        :x:/@split/0/-2?name
  catch

    /*
     * Silent catch, since exception implies database/table didn't exist.
     */





/*
 * Evaluating startup file.
 */
micro.evaluate.file:/modules/{0}/startup.hl
  :x:/@split/0/-2?name
