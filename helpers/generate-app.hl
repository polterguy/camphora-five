
/*
 * File responsible for generating app.
 *
 * Requires a [filename] argument, declaring which app to generate.
 * Optionally pass in a [delete-data] argument, and set its value to boolean "true"
 * if you wish to delete existing data/database/table for app.
 */





/*
 * Sanity check.
 */
micro.lambda.contract.min:x:/..
  filename:string





/*
 * Figuring out app's folder name.
 */
split:x:/../*/filename?value
  =:/
  =:.





/*
 * Checking if folder exists from before, and if it does, we delete it.
 */
if
  fetch:x:/0/0?value
    folder-exists:/modules/{0}/
      :x:/@split/0/-2?name
  delete-folder:/modules/{0}/
    :x:/@split/0/-2?name





/*
 * Creating folder structure for our app.
 */
create-folder:/modules/{0}/
  :x:/@split/0/-2?name





/*
 * Copying all files and folders from "template" folder into app's folder.
 */
list-files:@CAMPHORA/template/
copy-file:x:/@list-files/*?name
  dest:/modules/{0}/
    :x:/@split/0/-2?name
list-folders:@CAMPHORA/template/
for-each:x:/-/*?name
  split:x:/@_dp?value
    =:/
  copy-folder:x:/@_dp?value
    dest:/modules/{0}/{1}/
      :x:/..for-each/@split/0/-2?name
      :x:/@split/0/-?name





/*
 * Copying file that declares our app into folder for app.
 */
copy-file:x:/../*/filename?value
  dest:/modules/{0}/helpers/app-manifest.hl
    :x:/@split/0/-2?name





/*
 * Trying to delete old database table, if it exists, and if caller told us to do so.
 */
if:x:/../*/delete-data?value
  try
    p5.mysql.connect:[camphora]
      p5.mysql.execute:@"drop table `{0}`"
        :x:/@split/0/-2?name
  catch

    /*
     * Silent catch, since exception implies database/table didn't exist.
     */





/*
 * Creating "camphora-common" component, which is shared among all camphora 
 * generated apps.
 */
if
  fetch:x:/0/0?value
    folder-exists:/modules/camphora-common/
  delete-folder:/modules/camphora-common/





/*
 * Copying "/camphora-common/" folder, such that it becomes a module.
 */
copy-folder:@CAMPHORA/helpers/camphora-common/
  dest:/modules/camphora-common/





/*
 * Evaluating "camphora-common" startup file.
 */
micro.evaluate.file:/modules/camphora-common/startup.hl





/*
 * Evaluating startup file.
 */
micro.evaluate.file:/modules/{0}/startup.hl
  :x:/@split/0/-2?name
