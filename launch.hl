/*
 * Launches Camphora Five, by creating a wireframe, and a datagrid containing
 * all your CRUD manifests.
 */





/*
 * Making sure user has a folder where we can store his CRUD app
 * manifest files, if it doesn't exist from before.
 */
.first:bool:false
if
  fetch:x:/0/0?value
    folder-exists:~/documents/private/camphora/
  not

  /*
   * Camphora's app declaration folder doesn't exist, making sure we create it.
   */
  create-folder:~/documents/private/camphora/

  /*
   * Also adding all "template apps" from Camphora's app-template folder.
   */
  list-files:@CAMPHORA/app-templates/
    filter:.hl
  copy-file:x:/@list-files/*?name
    dest:~/documents/private/camphora/

  /*
   * Automatically launching help system, since this is user's first visit.
   */
  set:x:/@.first?value
    src:bool:true





/*
 * Setting page title.
 */
p5.web.page.set-title:Camphora Five





/*
 * Including Micro CSS.
 */
micro.css.include





/*
 * Creating main wire-frame for Camphora.
 */
create-widget:camphora-main-container
  class:container
  events


    /*
     * Help context Active Event.
     */
    desktop.help.get-context
      return:@CAMPHORA/help-files/Camphora Five/index.hl


  widgets
    div
      class:row
      widgets

        /*
         * Wrapper for toolbar at the top.
         */
        div
          class:col-100
          widgets
            div
              class:right
              widgets
                div
                  class:strip toolbar
                  widgets

                    /*
                     * Create new CRUD app manifest button.
                     */
                    button
                      innerValue:@"<span class=""icon-plus""></span>"
                      class:large
                      title:Create new CRUD app
                      onclick

                        /*
                         * Creating a new app by invoking file responsible for starting process.
                         *
                         * Notice, this is the same Hyperlambda file as we use for editing existing apps.
                         * Only difference is that it doesn't take an [file] parameter, which sets it
                         * into "new app mode".
                         */
                        micro.evaluate.file:@CAMPHORA/helpers/edit-app.hl

                    /*
                     * Upload existing CRUD app manifest file button.
                     */
                    micro.widgets.upload-button
                      title:Upload app manifest file
                      accept:.hl
                      multiple
                      .onupload

                        /*
                         * Saving file(s) by invoking file responsible for handling uploads to system.
                         */
                        add:x:/+
                          src:x:/../*/files
                        micro.evaluate.file:@CAMPHORA/helpers/upload-app.hl

                        /*
                         * Evaluating file responsible for re-databinding grid.
                         */
                        micro.evaluate.file:@CAMPHORA/helpers/databind-grid.hl

                    /*
                     * Home "button".
                     */
                    a
                      innerValue:@"<span class=""icon-home3""></span>"
                      class:button
                      href:/

        /*
         * Wrapper for datagrid containing all existing CRUD app manifest files.
         */
        div
          class:col-100
          widgets
            div
              class:shaded rounded air-inner
              widgets
                h3
                  innerValue:CRUD app manifests

                /*
                 * This is the actual grid wrapping all app manifest files.
                 */
                micro.widgets.grid:camphora-apps
                  class:striped hover
                  oninit

                    /*
                     * Databinding grid initially.
                     */
                    micro.evaluate.file:@CAMPHORA/helpers/databind-grid.hl





/*
 * Checking if this is user's first visit, and if so, launching help system
 * automatically.
 */
if:x:/@.first?value
  desktop.help.display