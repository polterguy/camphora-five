
/*
 * File responsible for starting Camphora.
 */





/*
 * Including Micro, adding Awesome Fonts, and serious skin - In addition to 
 * custom CSS file for app.
 */
p5.web.include-css-file:@MICRO/media/main.css
p5.web.include-css-file:@MICRO/media/fonts.css
p5.web.include-css-file:@MICRO/media/skins/serious.css
p5.web.include-css-file:@CAMPHORA/media/main.css





/*
 * Verifying user is logged in as root, and if not, 
 * forcing the user to login before we proceed.
 */
whoami
if:x:/@whoami/*/role?value
  !=:root

  /*
   * User is not logged in as root.
   */
  p5.core.login
    message:You'll need to login with a root account to access this module

  /*
   * Returning early to abort evaluating the rest of our file.
   */
  return





/*
 * Making sure we have at least version 1.4 of Hypereval installed, before proceeding,
 * since Camphora is dependent upon Hypereval to function.
 */
if
  hypereval.version
  <:decimal:1.4
  or
    hypereval.version
    not

  /*
   * Warning user that he has to install Hypereval 1.4 to continue.
   */
  create-widgets
    micro.widgets.modal:camphora-hypereval-warning
      widgets
        h3
          innerValue:Pre-requisites for Camphora not met
        p
          innerValue:Camphora Five is dependent upon Hypereval, version 1.4 or more to correctly function. Please install or upgrade Hypereval before continuing.
        div
          class:right
          widgets
            button
              innerValue:OK
              style:"margin-bottom:0;"
              oninit

                /*
                 * Setting initial focus to "OK" button.
                 */
                micro.page.set-focus:x:/../*/_event?value

              onclick

                /*
                 * Redirecting user to root URL.
                 */
                p5.web.get-root-location
                p5.web.set-location:x:/-?value

  /*
   * Returning early to abort evaluation of the rest of our file, since required
   * Hypereval version is not installed.
   */
  return





/*
 * Making sure user has a folder where we can store his apps, if it doesn't
 * exist from before.
 */
if
  fetch:x:/0/0?value
    folder-exists:~/documents/private/camphora/
  not

  /*
   * Camphora's app declaration folder doesn't exist, making sure we create it.
   */
  create-folder:~/documents/private/camphora/





/*
 * Creating main content container.
 */
create-container-widget:camphora-main-container
  class:container





/*
 * Creating main wire-frame for Camphora, making sure we also set page's title 
 * in the process.
 */
p5.web.page.set-title:Camphora Five
create-widget
  parent:camphora-main-container
  class:row
  widgets
    div
      class:col-100
      widgets
        div
          class:right
          widgets
            div
              class:strip toolbar
              style:"display:inline-block;"
              widgets
                button
                  innerValue:@"<span class=""icon-plus""></span>"
                  style:"padding-left:45px;padding-right:45px;"
                  onclick

                    /*
                     * Creating a new app by invoking file responsible for starting process.
                     *
                     * Notice, this is the same Hyperlambda file as we use for editing existing apps.
                     * Only difference is that it doesn't take an [file] parameter, which sets it
                     * into "new app mode".
                     */
                    micro.evaluate.file:@CAMPHORA/helpers/edit-app.hl

                button
                  innerValue:@"<span class=""icon-home3""></span>"
                  onclick

                    /*
                     * Making sure we get href attribute correctly.
                     */
                    p5.web.get-root-location
                    p5.web.set-location:x:/-?value

    div
      class:col-100
      widgets
        div
          class:shaded rounded air-inner bg
          style:"position:relative;"
          widgets
            a
              href:"https://github.com/polterguy/camphora-five"
              innerValue:@"<span class=""icon-question""></span>"
              target:_blank
              style:"position:absolute;top:.5rem;right:1rem;font-size:1.5rem;"
            h3
              innerValue:CRUD apps
            micro.widgets.grid:camphora-apps
              style:"margin-bottom:0px;"
              oninit

                /*
                 * Databinding grid initially.
                 */
                micro.evaluate.file:@CAMPHORA/helpers/databind-grid.hl
