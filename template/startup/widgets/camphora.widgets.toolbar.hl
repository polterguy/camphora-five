/*
 * This is a _"template file"_, implying this file will be copied into
 * your Camphora Five generated app's folder when your app is generated,
 * and possibly modified to reflect your app's name/structure.
 *
 * This file creates the __[camphora.xxx.widgets.toolbar]__ extension widget.
 */

/*
 * This extension widget allows you to display your Camphora Five
 * CRUD app as a widget, injecting the entire app, anywhere on your
 * page. The extension widget takes the following arguments.
 *
 * - __[datagrid]__ - The widget id of the __[camphora.xxx.widgets.datagrid]__ instance this toolbar co-exists with.
 * This argument must be the ID of a datagrid on your page.
 * - __[query]__ - Optional initial query for filter textbox.
 * - __[main-toolbar]__ - Whether or not the main toolbar should be created or not. The default value of this argument is _"false"_.
 * - __[filter-toolbar]__ - Whether or not the filter toolbar should be created or not. The default value of this argument is _"true"_.
 *
 * The above __[main-toolbar]__ argument can optionally be given the following sub arguments, which is only applied
 * if the toolbar is actually displayed.
 *
 * - __[import]__ - Whether or not user is allowed to import items (to CSV file). The default value for this argument is boolean _"false"_.
 * - __[export]__ - Whether or not user is allowed to export items (to CSV file). The default value for this argument is boolean _"false"_.
 *
 * The above __[filter-toolbar]__ argument can optionally be given the following sub arguments, which is only applied
 * if the toolbar is actually displayed.
 *
 * - __[filter]__ - Whether or not user is allowed to filter his items. The default value for this argument is boolean _"true"_.
 * - __[pager]__ - Whether or not user is allowed to page back and forth. The default value for this argument is boolean _"true"_.
 * - __[create]__ - Whether or not user is allowed to create items. The default value for this argument is boolean _"false"_.
 *
 * __Notice__, this event is dynamically created for each of your Camphora Five apps, doesn't exist
 * in its exact name. If your app's name is for instance _"address-book"_, you will have a widget named
 * __[camphora.address-book.widgets.toolbar]__ instead.
 */
create-event:camphora.widgets.toolbar

  /*
   * Sanity checking invocation.
   */
  micro.lambda.contract.min:x:/..
    datagrid:string
  micro.lambda.contract.optional:x:/..
    query:string
    main-toolbar:bool
    filter-toolbar:bool

  /*
   * Defaults.
   */
  .defaults
    main-toolbar:bool:false
      import:bool:false
      export:bool:false
    filter-toolbar:bool:true
      filter:bool:true
      pager:bool:true
      create:bool:false

  /*
   * Checking if caller does not want a main toolbar, at which
   * point we delete the main toolbar entirely from the return widget.
   */
  if:x:(/../*/main-toolbar|/@.defaults/*/main-toolbar)/$?value.bool
    =:bool:false

    /*
     * Removing main toolbar.
     */
    set:x:/../*/return/*/div/*/widgets/0

  else

    /*
     * Checking if caller does not want to allow editing of items, at
     * which point we remove the "upload CSV file" button, and the
     * "delete all items" button.
     */
    if:x:(/../*/main-toolbar/*/import|/@.defaults/*/main-toolbar/*/import)/$?value.bool
      =:bool:false

      /*
       * Removing import button.
       */
      set:x:/../*/return/*/div/*/widgets/0/*/widgets/*/*(/.import)/.

    /*
     * Checking if caller does not want to allow downloading of items, at
     * which point we remove the "download CSV file" button.
     */
    if:x:(/../*/main-toolbar/*/export|/@.defaults/*/main-toolbar/*/export)/$?value.bool
      =:bool:false

      /*
       * Removing export button.
       */
      set:x:/../*/return/*/div/*/widgets/0/*/widgets/*/*(/.export)/.

  /*
   * Checking if caller does not want a main toolbar, at which
   * point we delete the main toolbar entirely from the return widget.
   */
  if:x:(/../*/filter-toolbar|/@.defaults/*/filter-toolbar)/$?value.bool
    =:bool:false

    /*
     * Removing filter toolbar.
     */
    set:x:/../*/return/*/div/*/widgets/0/-

  else

    /*
     * Checking if caller does not want to allow filter items, at
     * which point we remove the filtering parts of the toolbar.
     */
    if:x:(/../*/filter-toolbar/*/filter|/@.defaults/*/filter-toolbar/*/filter)/$?value.bool
      =:bool:false

      /*
       * Removing export button.
       */
      set:x:/../*/return/*/div/*/widgets/0/-/*/widgets/*/*(/.filtering)/.

    /*
     * Checking if caller does not want to allow paging, at
     * which point we remove the paging parts of the toolbar.
     */
    if:x:(/../*/filter-toolbar/*/pager|/@.defaults/*/filter-toolbar/*/pager)/$?value.bool
      =:bool:false

      /*
       * Removing pager button.
       */
      set:x:/../*/return/*/div/*/widgets/0/-/*/widgets/*/*(/.pager)/.

    /*
     * Checking if caller does not want to allow paging, at
     * which point we remove the paging parts of the toolbar.
     */
    if:x:(/../*/filter-toolbar/*/create|/@.defaults/*/filter-toolbar/*/create)/$?value.bool
      =:bool:false

      /*
       * Removing pager button.
       */
      set:x:/../*/return/*/div/*/widgets/0/-/*/widgets/*/*(/.create)/.

  /*
   * Returning our toolbar widget to caller.
   */
  eval-x:x:/+/**(/.datagrid|/.query)
  return
    div
      .toolbar
      widgets
        div
          class:strip right toolbar
          widgets


            /*
             * Download records as a CSV file button.
             */
            button
              innerValue:@"<span class=""icon-download2""></span>"
              title:Download items as CSV file
              .export
              events


                /*
                 * Handled to make sure we enable/disable button if there
                 * are no items in current filter.
                 */
                camphora.datagrid.databound

                  /*
                   * Checking if there actually are any items in datagrid,
                   * and acting accordingly.
                   */
                  if:x:/../*/count?value.long
                    =:long:0

                    /*
                     * No items, disabling button.
                     */
                    set-widget-property:x:/../*/_event?value
                      disabled

                  else

                    /*
                     * 1 or more items, enabling button.
                     */
                    delete-widget-property:x:/../*/_event?value
                      disabled

              onclick

                /*
                 * Invoking widget lambda event that is responsible for
                 * downloading the database as a CSV file.
                 */
                .datagrid:x:/../*/datagrid?value
                camphora.download-items:x:/-?value


            /*
             * Upload CSV file button.
             */
            micro.widgets.upload-button
              title:Import a CSV file from your local disc
              accept:.csv
              .import
              .onupload

                /*
                 * Invoking widget lambda event that is responsible for
                 * importing a CSV file into our database.
                 */
                .datagrid:x:/../*/datagrid?value
                eval-x:x:/+/*
                camphora.database.import-csv-file:x:/-2?value
                  datagrid:x:/@.datagrid?value
                  file:x:/../*/files/*/physical-file?value

                /*
                 * Finding filter textbox and setting its [innerValue] to empty string.
                 */
                p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                  .toolbar
                p5.web.widgets.find-first:x:/-/*/*?value
                  .filter
                set-widget-property:x:/-/*/*?value
                  value:

                /*
                 * Setting focus to search textbox, to make it simpler to start
                 * filtering records, immediately after import.
                 */
                micro.page.set-focus:x:/@p5.web.widgets.find-first/*/*?value


            a
              role:button
              class:button
              innerValue:@"<span class=""icon-home3""></span>"
              title:Return to home
              href:/


        /*
         * Pager toolbar, containing paging buttons and
         * filter textbox and button.
         */
        div
          class:strip right
          widgets


            /*
             * Add new record button.
             */
            button
              innerValue:@"<span class=""icon-plus""></span>"
              class:large
              title:Create new record
              .create
              onclick

                /*
                 * Invoking widget lambda event that is responsible for
                 * creating a new item.
                 */
                .datagrid:x:/../*/datagrid?value
                camphora.create-new-item:x:/-?value


            /*
             * Search textbox.
             */
            input:camphora-search-query
              type:text
              placeholder:Search ...
              .filter
              .filtering
              onkeydown:@"if (event.keyCode == 13) {p5.$('camphora-search-button').raise('onclick');return false;}"
              events


                /*
                 * Active Event sink invoked when for some reasons the filter textbox
                 * should get focus.
                 */
                camphora.toolbar._set-focus-to-filter-textbox

                  /*
                   * Checking that this is our guy.
                   */
                  .datagrid:x:/../*/datagrid?value
                  if:x:/../*/_arg?value
                    !=:x:/@.datagrid?value
                    return
                  micro.page.set-focus:x:/../*/_event?value

              oninit

                /*
                 * Forward evaluated above.
                 */
                .query:x:/../*/query?value

                /*
                 * Sets the initial filter of our filter textbox, if any.
                 */
                if:x:/@.query?value
                  set-widget-property:x:/../*/_event?value
                    value:x:/@.query?value

                /*
                 * Setting initial focus to search textbox.
                 */
                micro.page.set-focus:x:/../*/_event?value


            /*
             * Search button.
             */
            button:camphora-search-button
              innerValue:@"<span class=""icon-search""></span>"
              title:Search for items
              .filtering
              onclick

                /*
                 * Retrieving search query.
                 */
                get-widget-property:camphora-search-query
                  value

                /*
                 * Re-databinding datagrid, now with a search filter condition.
                 */
                .datagrid:x:/../*/datagrid?value
                eval-x:x:/+/*
                camphora.datagrid.search:x:/@.datagrid?value
                  filter:x:/@get-widget-property/*/*?value

                /*
                 * Setting focus to search button.
                 */
                micro.page.set-focus:camphora-search-query


            /*
             * Page to previous page button.
             */
            button
              innerValue:@"<span class=""icon-arrow-left""></span>"
              title:Previous page
              .pager
              events


                /*
                 * Invoked when the [_arg]Â datagrid has been databound.
                 *
                 * Will be given [page] and [more-pages], indicating the current page,
                 * and whether or not there are more pages in the dataset currently
                 * active in the datagrid.
                 */
                camphora.datagrid.databound

                  /*
                   * Verifying that this invocation is for the current datagrid.
                   */
                  .datagrid:x:/../*/datagrid?value
                  if:x:/-?value
                    !=:x:/../*/_arg?value
                    return

                  /*
                   * Setting button's disabled property according to [_arg]
                   * value supplied by caller.
                   */
                  if:x:/../*/page?value
                    =:long:0

                    /*
                     * There are no more previous pages in datagrid.
                     */
                    set-widget-property:x:/../*/_event?value
                      disabled

                  else

                    /*
                     * There exists previous pages in datagrid.
                     */
                    delete-widget-property:x:/../*/_event?value
                      disabled


              onclick

                /*
                 * Paging to previous page.
                 */
                .datagrid:x:/../*/datagrid?value
                camphora.datagrid.page.previous:x:/-?value


            /*
             * Page to next page button.
             */
            button:camphora-next-button
              innerValue:@"<span class=""icon-arrow-right""></span>"
              title:Next page
              .pager
              events


                /*
                 * Invoked when the datagrid has been databound.
                 *
                 * Will be given [page] and [more-pages], indicating the current page,
                 * and whether or not there are more pages in the dataset currently
                 * active in the datagrid.
                 */
                camphora.datagrid.databound

                  /*
                   * Verifying that this invocation is for the current datagrid.
                   */
                  .datagrid:x:/../*/datagrid?value
                  if:x:/-?value
                    !=:x:/../*/_arg?value
                    return

                  /*
                   * Setting button's disabled property according to [_arg]
                   * value supplied by caller.
                   */
                  if:x:/../*/more-pages?value
                    =:bool:false

                    /*
                     * There are no more previous pages in datagrid.
                     */
                    set-widget-property:x:/../*/_event?value
                      disabled

                  else

                    /*
                     * There exists previous pages in datagrid.
                     */
                    delete-widget-property:x:/../*/_event?value
                      disabled


              onclick

                /*
                 * Paging to next page.
                 */
                .datagrid:x:/../*/datagrid?value
                camphora.datagrid.databind.next:x:/-?value
