/*
 * This file creates the __[camphora.widgets.toolbar.xxx]__ extension widget.
 */

/*
 * This extension widget allows you to display your Camphora Five
 * CRUD app as a widget, injecting the entire app, anywhere on your
 * page. The extension widget takes the following arguments.
 *
 * - __[edit-items]__ - Whether or not editing (and hence deletion) of items should be allowed. The default value for this argument is boolean _"false"_.
 * - __[download-items]__ - Whether or not downloading of items to CSV file should be allowed. The default value for this argument is boolean _"false"_.
 * - __[main-toolbar]__ - Whether or not the main toolbar should be created or not. The default value of this argument is _"false"_.
 */
create-event:camphora.widgets.toolbar.{0}
  :{app-name}

  /*
   * Defaults.
   */
  .defaults
    main-toolbar:bool:false
    edit-items:bool:false
    download-items:bool:false

  /*
   * Checking if caller does not want a main toolbar, at which
   * point we delete the main toolbar entirely from the return widget.
   */
  if:x:(/../*/main-toolbar|/@.defaults/*/main-toolbar)/$?value.bool
    =:bool:false

    /*
     * Removing main toolbar.
     */
    set:x:/../*/return/*/div/*/widgets/0

  else

    /*
     * Checking if caller does not want to allow editing of items, at
     * which point we remove the "upload CSV file" button, and the
     * "delete all items" button.
     */
    if:x:(/../*/edit-items|/@.defaults/*/edit-items)/$?value.bool
      =:bool:false

      /*
       * Removing main toolbar.
       */
      set:x:/../*/return/*/div/*/widgets/0/*/widgets/*/*(/.edit-items)/.

    /*
     * Checking if caller does not want to allow downloading of items, at
     * which point we remove the "download CSV file" button.
     */
    if:x:(/../*/download-items|/@.defaults/*/download-items)/$?value.bool
      =:bool:false

      /*
       * Removing main toolbar.
       */
      set:x:/../*/return/*/div/*/widgets/0/*/widgets/*/*(/.download-items)/.

  /*
   * Returning our toolbar widget to caller.
   */
  return
    div
      widgets
        div
          class:strip right toolbar
          widgets


            /*
             * Download records as a CSV file button.
             */
            button
              innerValue:@"<span class=""icon-download2""></span>"
              title:Download items as CSV file
              .download-items
              onclick

                /*
                 * Invoking widget lambda event that is responsible for
                 * downloading the database as a CSV file.
                 */
                camphora.download-items


            /*
             * Upload CSV file button.
             */
            micro.widgets.upload-button
              title:Import a CSV file from your local disc
              accept:.csv
              .edit-items
              .onupload

                /*
                 * Invoking widget lambda event that is responsible for
                 * importing a CSV file into our database.
                 */
                camphora.database.import-csv-file:x:/../*/files/*/physical-file?value


            /*
             * Delete all items from database button.
             */
            button
              innerValue:@"<span class=""icon-bin""></span>"
              title:Deletes all items in your database
              .edit-items
              onclick

                /*
                 * Invoking widget lambda event that is responsible for
                 * deleting all items from our database.
                 */
                camphora.database.delete-all-items


            a
              role:button
              class:button
              innerValue:@"<span class=""icon-home3""></span>"
              title:Return to home
              href:/


        /*
         * Pager toolbar, containing paging buttons and
         * filter textbox and button.
         */
        div
          class:strip right
          widgets


            /*
             * Add new record button.
             */
            button
              innerValue:@"<span class=""icon-plus""></span>"
              class:large
              title:Create new record
              onclick

                /*
                 * Invoking widget lambda event that is responsible for
                 * creating a new item.
                 */
                camphora.create-new-item


            /*
             * Search textbox.
             */
            input:camphora-search-query
              type:text
              placeholder:Search ...
              onkeydown:@"if (event.keyCode == 13) {p5.$('camphora-search-button').raise('onclick');return false;}"
              events


                /*
                 * Invoked when for some reasons the number of items have been
                 * changed in the database. The new item count will be passed
                 * in as [_arg].
                 *
                 * In this widget lambda event, we simply set the placeholder's
                 * value of the filter textbox such that it displays the total
                 * number of items.
                 */
                camphora.count-changed

                  /*
                   * Modifying placeholder to contain record count.
                   */
                  set-widget-property:x:/../*/_event?value
                    placeholder:Search amongst {0} {1} records ...
                      :x:/../*/_arg?value
                      :{app-name}


                /*
                 * Invoked when for some reasons the currently active filter
                 * has been changed. The new search query will be given as [_arg].
                 */
                camphora.filter-changed

                  /*
                   * Setting widget's [value] to the given [_arg].
                   */
                  set-widget-property:x:/../*/_event?value
                    value:x:/../*/_arg?value


              oninit

                /*
                 * Making sure we set initial count of records.
                 */
                p5.mysql.connect:[camphora]
                  p5.mysql.scalar:select count(*) from `{0}`
                    :{app-name}
                  camphora.count-changed:x:/@p5.mysql.scalar?value

                /*
                 * Setting initial focus to search textbox.
                 */
                micro.page.set-focus:x:/../*/_event?value


            /*
             * Search button.
             */
            button:camphora-search-button
              innerValue:@"<span class=""icon-search""></span>"
              title:Search for items
              onclick

                /*
                 * Retrieving search query.
                 */
                get-widget-property:camphora-search-query
                  value

                /*
                 * Re-databinding datagrid, now with a search filter condition.
                 */
                camphora.datagrid.search:x:/@get-widget-property/*/*?value

                /*
                 * Setting focus to search button.
                 */
                micro.page.set-focus:camphora-search-query


            /*
             * Page to previous page button.
             */
            button
              innerValue:@"<span class=""icon-arrow-left""></span>"
              title:Previous page
              events


                /*
                 * Invoked when the datagrid has been databound.
                 *
                 * Will be given [page] and [more-pages], indicating the current page,
                 * and whether or not there are more pages in the dataset currently
                 * active in the datagrid.
                 */
                camphora.datagrid.databound

                  /*
                   * Setting button's disabled property according to [_arg]
                   * value supplied by caller.
                   */
                  if:x:/../*/page?value
                    =:long:0

                    /*
                     * There are no more previous pages in datagrid.
                     */
                    set-widget-property:x:/../*/_event?value
                      disabled

                  else

                    /*
                     * There exists previous pages in datagrid.
                     */
                    delete-widget-property:x:/../*/_event?value
                      disabled


              onclick

                /*
                 * Paging to previous page.
                 */
                camphora.datagrid.page.previous


            /*
             * Page to next page button.
             */
            button:camphora-next-button
              innerValue:@"<span class=""icon-arrow-right""></span>"
              title:Next page
              events


                /*
                 * Invoked when the datagrid has been databound.
                 *
                 * Will be given [page] and [more-pages], indicating the current page,
                 * and whether or not there are more pages in the dataset currently
                 * active in the datagrid.
                 */
                camphora.datagrid.databound

                  /*
                   * Setting button's disabled property according to [_arg]
                   * value supplied by caller.
                   */
                  if:x:/../*/more-pages?value
                    =:bool:false

                    /*
                     * There are no more previous pages in datagrid.
                     */
                    set-widget-property:x:/../*/_event?value
                      disabled

                  else

                    /*
                     * There exists previous pages in datagrid.
                     */
                    delete-widget-property:x:/../*/_event?value
                      disabled


              onclick

                /*
                 * Paging to next page.
                 */
                camphora.datagrid.databind.next
