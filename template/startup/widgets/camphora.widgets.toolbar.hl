/*
 * This file creates the __[camphora.widgets.toolbar.xxx]__ extension widget.
 */

/*
 * This extension widget allows you to display your Camphora Five
 * CRUD app as a widget, injecting the entire app, anywhere on your
 * page. The extension widget takes the following arguments.
 *
 * - __[main-toolbar]__ - Whether or not the main toolbar should be created or not. The default value of this argument is _"false"_.
 */
create-event:camphora.widgets.toolbar.{0}
  :{app-name}

  /*
   * Defaults.
   */
  .defaults
    main-toolbar:bool:false

  /*
   * Checking if caller does not want a main toolbar, at which
   * point we delete it from the return value.
   */
  if:x:(/../*/main-toolbar|/@.defaults/*/main-toolbar)/$?value.bool
    =:bool:false

    /*
     * Removing main toolbar.
     */
    set:x:/../*/return/*/div/*/widgets/0

  /*
   * Returning our toolbar widget to caller.
   */
  return
    div
      widgets
        div
          class:strip right toolbar
          widgets


            /*
             * Download records as a CSV file button.
             */
            button
              innerValue:@"<span class=""icon-download2""></span>"
              title:Download items as CSV file
              onclick

                /*
                 * Invoking widget lambda event that is responsible for
                 * downloading the database as a CSV file.
                 */
                camphora.download-items


            /*
             * Upload CSV file button.
             */
            micro.widgets.upload-button
              title:Import a CSV file from your local disc
              accept:.csv
              .onupload

                /*
                 * Invoking widget lambda event that is responsible for
                 * importing a CSV file into our database.
                 */
                camphora.database.import-csv-file:x:/../*/files/*/physical-file?value


            /*
             * Delete all items from database button.
             */
            button
              innerValue:@"<span class=""icon-bin""></span>"
              title:Deletes all items in your database
              onclick

                /*
                 * Invoking widget lambda event that is responsible for
                 * deleting all items from our database.
                 */
                camphora.database.delete-all-items


            a
              role:button
              class:button
              innerValue:@"<span class=""icon-home3""></span>"
              title:Return to home
              href:/


        /*
         * Pager toolbar, containing paging buttons and
         * filter textbox and button.
         */
        div
          class:strip right
          widgets


            /*
             * Search textbox.
             */
            input:camphora-search-query
              type:text
              placeholder:Search ...
              onkeydown:@"if (event.keyCode == 13) {p5.$('camphora-search-button').raise('onclick');return false;}"
              events


                /*
                 * Invoked when for some reasons the number of items have been
                 * changed in the database.
                 *
                 * In this widget lambda event, we simply setthe placeholder's
                 * value of the filter textbox such that it displays the total
                 * number of items.
                 */
                camphora.count-changed

                  /*
                   * Modifying placeholder to contain record count.
                   */
                  p5.mysql.connect:[camphora]
                    p5.mysql.scalar:@"select count(*) from `{0}`"
                      :{app-name}
                    set-widget-property:x:/../*/_event?value
                      placeholder:Search amongst {0} records ...
                        :x:/@p5.mysql.scalar?value


              oninit

                /*
                 * Setting focus initially to search textbox, and
                 * making sure we set initial count of records.
                 */
                camphora.count-changed
                micro.page.set-focus:x:/../*/_event?value


            /*
             * Search button.
             */
            button:camphora-search-button
              innerValue:@"<span class=""icon-search""></span>"
              title:Search for items
              onclick

                /*
                 * Retrieving search query.
                 */
                get-widget-property:camphora-search-query
                  value

                /*
                 * Re-databinding datagrid, now with a search filter condition.
                 */
                camphora.datagrid.search:x:/@get-widget-property/*/*?value

                /*
                 * Setting focus to search button.
                 */
                micro.page.set-focus:camphora-search-query


            /*
             * Page to previous page button.
             */
            button
              innerValue:@"<span class=""icon-arrow-left""></span>"
              title:Previous page
              events


                /*
                 * Invoked when the currently viewed page of the datagrid has changed.
                 *
                 * Will be given [page] and [more-pages], indicating the current page,
                 * and whether or not there are more pages in the dataset currently
                 * active in the datagrid.
                 */
                camphora.datagrid.page-changed

                  /*
                   * Setting button's disabled property according to [_arg]
                   * value supplied by caller.
                   */
                  if:x:/../*/page?value
                    =:long:0

                    /*
                     * There are no more previous pages in datagrid.
                     */
                    set-widget-property:x:/../*/_event?value
                      disabled

                  else

                    /*
                     * There exists previous pages in datagrid.
                     */
                    delete-widget-property:x:/../*/_event?value
                      disabled


              onclick

                /*
                 * Paging to previous page.
                 */
                camphora.datagrid.page.previous


            /*
             * Page to next page button.
             */
            button:camphora-next-button
              innerValue:@"<span class=""icon-arrow-right""></span>"
              title:Next page
              events


                /*
                 * Invoked when the currently viewed page of the datagrid has changed.
                 *
                 * Will be given [page] and [more-pages], indicating the current page,
                 * and whether or not there are more pages in the dataset currently
                 * active in the datagrid.
                 */
                camphora.datagrid.page-changed

                  /*
                   * Setting button's disabled property according to [_arg]
                   * value supplied by caller.
                   */
                  if:x:/../*/more-pages?value
                    =:bool:false

                    /*
                     * There are no more previous pages in datagrid.
                     */
                    set-widget-property:x:/../*/_event?value
                      disabled

                  else

                    /*
                     * There exists previous pages in datagrid.
                     */
                    delete-widget-property:x:/../*/_event?value
                      disabled


              onclick

                /*
                 * Paging to next page.
                 */
                camphora.datagrid.databind.next


            /*
             * Add new record button.
             */
            button
              innerValue:@"<span class=""icon-plus""></span>"
              class:large
              title:Create new record
              onclick

                /*
                 * Invoking widget lambda event that is responsible for
                 * creating a new item.
                 */
                camphora.create-new-item
