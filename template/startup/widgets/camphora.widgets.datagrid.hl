/*
 * This file creates the __[camphora.widgets.crud.xxx]__ extension widget.
 */

/*
 * This extension widget allows you to display your Camphora Five
 * CRUD app as a widget, injecting the entire app, anywhere on your
 * page. The event optionally takes the following arguments.
 *
 * - __[edit-items]__ - Whether or not editing (and hence deletion) of items should be allowed. The default value for this argument is boolean _"false"_.
 * - __[page-size]__ - Number of items the datagrid should show at once. The default value is 25.
 * - __[query]__ - Optional initial search query to databind the datagrid towards.
 * - __[page]__ - Optional initial page to databind the datagrid towards.
 */
create-event:camphora.widgets.{0}.datagrid
  :{app-name}

  /*
   * Sanity checking invocation.
   */
  micro.lambda.contract.optional:x:/..
    edit-items:bool
    page-size:long
    query:string
    page:long

  /*
   * Defaults.
   */
  .defaults
    edit-items:bool:false
    page-size:long:10
    page:long:0

  /*
   * Checking if we should prevent editing (and deletion) of items.
   */
  if:x:(/../*/edit-items|/@.defaults/*/edit-items)/$?value.bool
    =:bool:false

    /*
     * Removing [.row]'s [onclick] event handler.
     */
    set:x:/../*/return/*/micro.widgets.mysql.datagrid/*/.row/*/onclick

  else

    /*
     * Making sure we create rows such that the user gets a visual clue that
     * he can click the rows in his grid.
     */
    add:x:/../*/return/*/micro.widgets.mysql.datagrid/*/.row
      src
        class:camphora-grid-editable

    /*
     * Adding "delete item" row.
     */
    add:x:/../*/return/*/micro.widgets.mysql.datagrid/*/columns
      src
        Delete
          .lambda
            eval-x:x:/+/*/*/onclick/*/.id
            return
              a
                href:#
                innerValue:@"<span class=""icon-bin""></span>"
                onclick

                  /*
                   * Forward evaluated above.
                   */
                  .id:x:/../*/row/*/id?value

                  /*
                   * Opening database connection.
                   */
                  p5.mysql.connect:[camphora]

                    /*
                     * Deleting item from database.
                     */
                    p5.mysql.delete:delete from `{0}` where id = @id
                      :{app-name}
                      @id:x:/@.id?value

                    /*
                     * Figuring out new count of items,and making sure we
                     * signal that the count has changed.
                     */
                    p5.mysql.scalar:@"select count(*) from `{0}`"
                      :{app-name}
                    camphora.count-changed:x:/-?value

                  /*
                   * Re-databinding datagrid, making sure we keep our current
                   * [filter], [page], [order-by] and [order-dir] arguments.
                   */
                  camphora.datagrid.databind

  /*
   * Checking if we should provide a default query.
   */
  if:x:/../*/query?value
    and:x:/../*/query?value
    !=:

    /*
     * Making sure our initial databind invocation contains our query.
     */
    set:x:/../*/return/*/*/oninit/*/.query?value
      src:x:/../*/query?value

  /*
   * Returning actual widget to caller, making sure we forward
   * evaluate any arguments.
   */
  eval-x:x:/+/*/*(/page-size|/oninit/*/.page)
  return
    micro.widgets.mysql.datagrid
      .row
        onclick

          /*
           * Evaluating file responsible for allowing the user to edit some item.
           */
          eval-x:x:/+/*
          micro.evaluate.file:/modules/{0}/helpers/edit-item.hl
            :{app-name}
            id:x:/../*/.row/*/id?value
            row-widget-id:x:/../*/_event?value

      database:camphora
      table:{app-name}
      page-size:x:(/../*/page-size|/@.defaults/*/page-size)/$?value.long
      class:hover striped camphora-datagrid
      databind:bool:false
      oninit

        /*
         * Added before instantiation.
         */
        .query
        .page:x:(/../*/page|/@.defaults/*/page)/$?value

        /*
         * Checking if caller provided an initial [query] argument, at which
         * point we databind the datagrid towards that given query, instead
         * of invoking the default databind event.
         */
        if:x:/@.query?value

          /*
           * Initial databind query was given.
           */
          eval-x:x:/+/*
          camphora.datagrid.search:x:/@.query?value
            page:x:/@.page?value

        else

          /*
           * Making sure we use our internal databind event, since we don't
           * databind the datagrid by default.
           *
           * This ensures that we get to correctly enable/disable our pager buttons,
           * since the databind widget lambda event checks the return value from
           * the MySQL datagrid's databind invocation, and acts accordingly.
           */
          eval-x:x:/+/*
          camphora.datagrid.databind
            page:x:/@.page?value


      /*
       * Our column definition.
       *
       * Notice, this one is dynamically built during the "generate" process,
       * when app is generated.
       */
      columns
        id
          visible:bool:false


      /*
       * Widget lambda events for our datagrid.
       *
       * This wraps the "API" for our datagrid.
       */
      events


        /*
         * Creates a new item by displaying a modal widget to
         * user, and allowing him or her to fill in the values
         * for the new item.
         */
        camphora.create-new-item

          /*
           * Evaluating file responsible for creating a new record.
           */
          micro.evaluate.file:/modules/{0}/helpers/create-item.hl
            :{app-name}


        /*
         * Downloads entire database of your Camphora app as a CSV file.
         */
        camphora.download-items

          /*
           * Evaluating file responsible for exporting items to client.
           */
          micro.evaluate.file:/modules/{0}/helpers/download-items.hl
            :{app-name}


        /*
         * Sorts the datagrid according to the given [column].
         *
         * This event will not remove the current [filter] or tamper
         * with it in any ways. This allows you to sort with the current
         * [filter] being active.
         */
        camphora.datagrid.sort

          /*
           * Sanity checking invocation.
           */
          micro.lambda.contract.min:x:/..
            column:string

          /*
           * Evaluating file responsible for creating a new record.
           */
          eval-x:x:/+/*/column
          micro.evaluate.file:/modules/{0}/helpers/sort-datagrid.hl
            :{app-name}
            column:x:/../*/column?value


        /*
         * Convenience event to avoid having to manually construct your own filter.
         *
         * Applies the given [_arg] as a [filter] condition to the datagrid,
         * to allow for searching through the datagrid in an easier fashion
         * than to manually having to construct the [filter] condition and
         * invoke databind manually.
         *
         * This widget lambda event will construct a "like" type of condition
         * for each textbox/textarea, and an "equal" type of condition for most
         * other fields in your Camphora app.
         *
         * All conditions will be OR'ed together, resulting in a [filter] to
         * our [camphora.datagrid.databind] invocation.
         */
        camphora.datagrid.search

          /*
           * Sanity checking invocation.
           */
          micro.lambda.contract.min:x:/..
            _arg:string
          micro.lambda.contract.optional:x:/..
            page:long

          /*
           * Checking if caller provided a [page] argument.
           */
          if:x:/../*/page?value
            add:x:/../*/micro.evaluate.file
              src:x:/../*/page

          /*
           * Evaluating file responsible for creating a new record.
           */
          eval-x:x:/+/*/query
          micro.evaluate.file:/modules/{0}/helpers/search-datagrid.hl
            :{app-name}
            query:x:/../*/_arg?value


        /*
         * Pages datagrid to previous page.
         */
        camphora.datagrid.page.previous

          /*
           * Decrementing page property, making sure we never go beneath zero.
           */
          camphora.datagrid.page
          if:x:/-/*?value
            =:long:0
            return
          -:x:/@camphora.datagrid.page/*?value
            _:1
          eval-x:x:/+/*
          camphora.datagrid.page
            value:x:/@-?value

          /*
           * Re-databinding datagrid.
           */
          camphora.datagrid.databind


        /*
         * Pages datagrid to next page.
         */
        camphora.datagrid.databind.next

          /*
           * Decrementing page property.
           */
          camphora.datagrid.page
          +:x:/-/*?value
            _:1
          eval-x:x:/+/*
          camphora.datagrid.page
            value:x:/@+?value

          /*
           * Re-databinding datagrid.
           */
          camphora.datagrid.databind


        /*
         * Databinds our datagrid.
         *
         * This is a more "low level" event than [camphora.datagrid.search],
         * and allows you to completely control every aspect of your filtering,
         * paging, etc, entirely if you wish.
         *
         * Optionally pass in [filter], [page], [order-by] and [order-dir].
         * If these arguments are not given, the event will determine the current
         * persistent property values of these arguments from the associated
         * property events.
         */
        camphora.datagrid.databind

          /*
           * Signal node to separate arguments from the rest of our lambda.
           */
          .signal

          /*
           * Sanity checking invocation.
           */
          micro.lambda.contract.optional:x:/..
            filter
            page
            order-by
            order-dir

          /*
           * Evaluating "databind grid" Hyperlambda file.
           */
          add:x:/+
            src:x:/@.signal/--/<-(!/_event)
          micro.evaluate.file:/modules/{0}/helpers/databind-grid.hl
            :{app-name}


        /*
         * Imports the specified [_arg]Â file into your Camphora app.
         *
         * Notice, this event expects the file given to be a CSV file,
         * and that it matches your columns from your Camphora app.
         *
         * It also expects the CSV file to have headers, implying the
         * first row in your CSV file must conform with your column
         * structure, as defined through its [field] values when creating
         * your app.
         */
        camphora.database.import-csv-file

          /*
           * Sanity checking invocation.
           */
          micro.lambda.contract.min:x:/..
            _arg:string

          /*
           * Evaluating "upload CSV file" Hyperlambda file.
           */
          eval-x:x:/+/*/file
          micro.evaluate.file:/modules/{0}/helpers/import-csv-file.hl
            :{app-name}
            file:x:/../*/_arg?value

          /*
           * Re-databinding datagrid, making sure we reset all filter,
           * and other properties for our datagrid.
           */
          camphora.datagrid.databind
            filter
            page
            order-by
            order-dir


        /*
         * Deletes all items from our database.
         */
        camphora.database.delete-all-items

          /*
           * Evaluates file responsible for actually deleting all items.
           */
          micro.evaluate.file:/modules/{0}/helpers/delete-all-items.hl
            :{app-name}


        /*
         * Updates an existing item in the database.
         *
         * Expects a collection of column/values as arguments, and at the minimum
         * an [id] argument, declaring which record id we should update - In addition
         * to at least one other argument, which must be a reference to a column in
         * our database.
         */
        camphora.database.update

          /*
           * Signal node to separate arguments from the rest of our lambda.
           */
          .signal

          /*
           * Sanity checking invocation.
           */
          micro.lambda.contract.min:x:/..
            id:string

          /*
           * Evaluating "update item" Hyperlambda file.
           */
          add:x:/+
            src:x:/@.signal/--/<-(!/_event)
          micro.evaluate.file:/modules/{0}/helpers/update-item.hl
            :{app-name}


        /*
         * Inserts a new item into the database.
         *
         * Expects a collection of column/values as arguments.
         * These arguments must be references to actual columns in your
         * database.
         */
        camphora.database.insert

          /*
           * Signal node to separate arguments from the rest of our lambda.
           */
          .signal

          /*
           * Evaluating "insert item" Hyperlambda file.
           */
          add:x:/+
            src:x:/@.signal/--/<-(!/_event)
          micro.evaluate.file:/modules/{0}/helpers/insert-item.hl
            :{app-name}


        /*
         * These next events are "properties" of your datagrid, such as its
         * current "page", "filter", and so on.
         */


        /*
         * Sets or gets the current [page] property for datagrid.
         */
        camphora.datagrid.page

          /*
           * Checking if this is a "set" invocation or a "get" invocation.
           */
          if:x:/../*/value

            /*
             * Then checking if it's not a 'set to null' invocation.
             */
            if:x:/../*/value?value

              /*
               * Some actual value for our property value.
               */
              p5.web.viewstate.set:camphora.datagrid.page
                src:x:/../*/value?value

            else

              /*
               * 'Set to null' invocation.
               */
              p5.web.viewstate.set:camphora.datagrid.page

          else

            /*
             * "get" invocation, returning current value to caller.
             */
            p5.web.viewstate.get:camphora.datagrid.page
            if:x:/-/*?value

              /*
               * We have a current value for our "page" property.
               */
              eval-x:x:/+/*
              return
                page:x:/@p5.web.viewstate.get/*?value.long

            else

              /*
               * We don't have a current value for our "page" property.
               */
              return
                page:long:0


        /*
         * Sets or gets the current [order-by] property for datagrid.
         */
        camphora.datagrid.order-by

          /*
           * Checking if this is a "set" invocation or a "get" invocation.
           */
          if:x:/../*/value

            /*
             * Then checking if it's not a 'set to null' invocation.
             */
            if:x:/../*/value?value

              /*
               * Some actual value for our property value.
               */
              p5.web.viewstate.set:camphora.datagrid.order-by
                src:x:/../*/value?value

            else

              /*
               * 'Set to null' invocation.
               */
              p5.web.viewstate.set:camphora.datagrid.order-by

          else

            /*
             * "get" invocation, returning current value to caller, but
             * only if there actually is a value for property.
             */
            p5.web.viewstate.get:camphora.datagrid.order-by
            if:x:/-/*?value
              eval-x:x:/+/*
              return
                order-by:x:/@p5.web.viewstate.get/*?value


        /*
         * Sets or gets the current [order-dir] property for datagrid.
         */
        camphora.datagrid.order-dir

          /*
           * Checking if this is a "set" invocation or a "get" invocation.
           */
          if:x:/../*/value

            /*
             * Then checking if it's not a 'set to null' invocation.
             */
            if:x:/../*/value?value

              /*
               * Some actual value for our property value.
               */
              p5.web.viewstate.set:camphora.datagrid.order-dir
                src:x:/../*/value?value

            else

              /*
               * 'Set to null' invocation.
               */
              p5.web.viewstate.set:camphora.datagrid.order-dir

          else

            /*
             * "get" invocation, returning current value to caller, but
             * only if there actually is a value for property.
             */
            p5.web.viewstate.get:camphora.datagrid.order-dir
            if:x:/-/*?value
              eval-x:x:/+/*
              return
                order-dir:x:/@p5.web.viewstate.get/*?value


        /*
         * Sets or gets the current [filter] property for datagrid.
         */
        camphora.datagrid.filter

          /*
           * Checking if this is a "set" invocation or a "get" invocation.
           */
          if:x:/../*/value

            /*
             * Then checking if it's not a 'set to null' invocation.
             */
            if:x:/../*/value?value

              /*
               * Some actual value for our property value.
               */
              add:x:/+2/*/*
                src:x:/../*/value/*
              eval-x:x:/+/*/*
              p5.web.viewstate.set:camphora.datagrid.filter
                src
                  filter:x:/../*/value?value

            else

              /*
               * 'Set to null' invocation.
               */
              p5.web.viewstate.set:camphora.datagrid.filter

          else

            /*
             * "get" invocation, returning current value to caller, but
             * only if there actually is a value for property.
             */
            p5.web.viewstate.get:camphora.datagrid.filter
            if:x:/-/*/*
              add:x:/+
                src:x:/@p5.web.viewstate.get/*/*
              return
