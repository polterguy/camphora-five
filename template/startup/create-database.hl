/*
 * This file creates your Camphora Five module's database, unless it already exists from before.
 */





/*
 * Figuring out name of app.
 */
split:x:/..?name
  =:/
  =:.





/*
 * Loading app's declaration file, to make sure we can get the columns of our
 * database correct.
 */
_cols
_cols-changed
load-file:/{0}/{1}/helpers/app-manifest.hl
  :x:/@split/0?name
  :x:/@split/1?name
for-each:x:/@load-file/*/*/field

  /*
   * Appending currently iterated column to [_cols] above, according to
   * what [type] of [field] it is.
   */
  switch:x:/@_dp/#/*/type?value
    case:text
    case:radio
    case:select
      set:x:/@_cols?value
        src:{0}{1}
          :x:/@_cols?value
          :@"`{0}` varchar(512) COLLATE utf8mb4_unicode_ci NULL,
  "
            :x:/@_dp/#/*/name?value
      set:x:/@_cols-changed?value
        src:{0}{1}
          :x:/@_cols-changed?value
          :@"
  alter table `{0}` modify `{1}` varchar(512) COLLATE utf8mb4_unicode_ci NULL;
  alter table `{0}` add `{1}` varchar(512) COLLATE utf8mb4_unicode_ci NULL;"
            :x:/@split/1?name
            :x:/@_dp/#/*/name?value
    case:checkbox
    case:number
      set:x:/@_cols?value
        src:{0}{1}
          :x:/@_cols?value
          :@"`{0}` INT(10) NULL,
  "
            :x:/@_dp/#/*/name?value
      set:x:/@_cols-changed?value
        src:{0}{1}
          :x:/@_cols-changed?value
          :@"
  alter table `{0}` modify `{1}` INT(10) NULL;
  alter table `{0}` add `{1}` INT(10) NULL;"
            :x:/@split/1?name
            :x:/@_dp/#/*/name?value
    case:created
      set:x:/@_cols?value
        src:{0}{1}
          :x:/@_cols?value
          :@"`{0}` TIMESTAMP NOT NULL DEFAULT CURRENT_TIMESTAMP,
  "
            :x:/@_dp/#/*/name?value
      set:x:/@_cols-changed?value
        src:{0}{1}
          :x:/@_cols-changed?value
          :@"
  alter table `{0}` modify `{1}` TIMESTAMP NOT NULL DEFAULT CURRENT_TIMESTAMP;
  alter table `{0}` add `{1}` TIMESTAMP NOT NULL DEFAULT CURRENT_TIMESTAMP;"
            :x:/@split/1?name
            :x:/@_dp/#/*/name?value
    case:textarea
    case:markdown
      set:x:/@_cols?value
        src:{0}{1}
          :x:/@_cols?value
          :@"`{0}` longtext COLLATE utf8mb4_unicode_ci NULL,
  "
            :x:/@_dp/#/*/name?value
      set:x:/@_cols-changed?value
        src:{0}{1}
          :x:/@_cols-changed?value
          :@"
  alter table `{0}` modify `{1}` longtext COLLATE utf8mb4_unicode_ci NULL;
  alter table `{0}` add `{1}` longtext COLLATE utf8mb4_unicode_ci NULL;"
            :x:/@split/1?name
            :x:/@_dp/#/*/name?value





/*
 * Creating database for app, unless it already exists.
 */
try

  /*
   * Necessary to parametrize our schema declarations.
   */
  p5.config.get:p5.data.prefix

  /*
   * Notice, if this invocation throws an exception, it means that our database
   * doesn't exist, which we're catching further down, and handling correctly,
   * by creating our schema, and invoking "self" again recursively.
   */
  p5.mysql.connect:[camphora]

    /*
     * Will create the table if it doesn't exist.
     */
    p5.mysql.execute:@"
CREATE TABLE IF NOT EXISTS `{0}` (
  `id` int(10) unsigned NOT NULL AUTO_INCREMENT,
  {1}PRIMARY KEY (`id`),
  UNIQUE KEY `id_UNIQUE` (`id`)
) ENGINE=InnoDB AUTO_INCREMENT=0 DEFAULT CHARSET=utf8mb4 COLLATE=utf8mb4_unicode_ci;"
      :x:/@split/1?name
      :x:/@_cols?value

    /*
     * Creates a procedure that will modify our table, to add any missing columns.
     *
     * Notice, this is a little bit of a MySQL trickery, where we inform MySQL
     * that we simply want it to comtinue in case column already exists, instead of
     * aborting, and raising an exception.
     *
     * This implies that all our "ALTER TABLE" queries, created above will execute,
     * even if one of our columns already exists from before.
     */
    p5.mysql.execute:@"
create procedure {0}camphora_foo ()
begin
  declare continue handler for 1060 begin end;
  declare continue handler for 1054 begin end;{1}
end"
      :x:/@p5.config.get/*?value
      :x:/@_cols-changed?value

    /*
     * Making absolutely sure we're able to drop our procedure.
     */
    try

      /*
       * Invokes our "modify tables" procedure.
       */
      p5.mysql.execute:@"call {0}camphora_foo()"
        :x:/@p5.config.get/*?value

    finally

      /*
       * Drops our "modify table" procedure.
       */
      p5.mysql.execute:@"drop procedure {0}camphora_foo"
        :x:/@p5.config.get/*?value

catch

  /*
   * Attempting to create database, unless this is our second invocation, for then
   * to invoke self again, for simplicity reasons.
   */
  if:x:/../*/second?value
    not

    /*
     * Making sure we prepend the database prefix in front of schema name.
     */
    p5.config.get:p5.data.prefix

    /*
     * Creating database schema.
     */
    p5.mysql.connect:[sys]
      p5.mysql.execute:@"CREATE SCHEMA `{0}camphora`;"
        :x:/@p5.config.get/*?value

    /*
     * Invoking self, with [second] being true, to avoid never ending recursion.
     */
    micro.evaluate.file:x:/..?name
      second:bool:true

    /*
     * Returning early, to avoid evaluating the rest of our file twice.
     */
    return
