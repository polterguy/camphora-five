/*
 * This file is responsible for actually showing the main datagrid
 * for a Camphora Five app.
 */





/*
 * Retrieves URL entities, which we'll need later to know what the root folder
 * of our app is.
 */
micro.url.get-entities





/*
 * Changing the title of page.
 */
p5.web.page.set-title:{app-name}





/*
 * Including Micro's CSS files, in addition to our main CSS
 * file for currently evaluated Camphora app.
 */
micro.css.include
p5.web.include-css-file:/modules/{0}/media/main.css
  :x:/@micro.url.get-entities/0?name





/*
 * Creating main wire frame for app, containing toolbar and datagrid.
 */
create-widget:camphora-main-container
  class:container
  events


    /*
     * Help context Active Event.
     */
    desktop.help.get-context

      /*
       * Returning the root folder for Camphora's help files.
       */
      return:@CAMPHORA/help-files/Camphora Five/index.hl


    /*
     * Invoked when for some reasons the number of items have been
     * changed in the database. The new item count will be passed in
     * as [_arg].
     *
     * We simply update the title of our page, to reflect the number
     * of items in the database.
     */
    camphora.count-changed

      /*
       * Setting the title of our page, to reflect the number of items.
       */
      p5.web.page.set-title:{0} records of type {1}
        :x:/../*/_arg?value
        :{app-name}


  widgets

    /*
     * Row wrapping all toolbars for app.
     */
    div
      class:row
      widgets
        div
          class:col-100
          widgets:toolbar-widget

            /*
             * Our actual toolbar.
             *
             * Notice, the name of this node is dynamically replaced
             * as your app is generated, to the correct name for the
             * extension widget.
             *
             * We instantiate our toolbar widget by default such that
             * it also displays the home button, delete all button, import
             * CSV file button, etc. If you don't want these buttons, or
             * this strip to be more accurate, you can set the [main-toolbar]
             * value to "false".
             */
            camphora.widgets.toolbar
              main-toolbar:bool:true


    /*
     * Main row wrapping our MySQL datagrid.
     */
    div
      class:row air-top
      widgets
        div
          class:col-100
          widgets:crud-widget

            /*
             * Our actual datagrid.
             *
             * Notice, the name of this node is dynamically replaced
             * as your app is generated, to the correct name for the
             * extension widget.
             *
             * We instantiate the widget such that it allows for editing
             * and deletion of items. If you don't want this feature, you
             * can change the [edit-items] value to "false".
             */
            camphora.widgets.datagrid:camphora-datagrid
              edit-items:bool:true
