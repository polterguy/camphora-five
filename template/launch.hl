/*
 * Launcher file for Camphora Five generated app.
 */





/*
 * Retrieves our app's name, and other URL entities.
 */
camphora._internal.get-app-url-entities:x:/..





/*
 * Loading file containing app declaration.
 */
load-file:/modules/{0}/helpers/app-manifest.hl
  :x:/@camphora._internal.get-app-url-entities/0?name





/*
 * Checking if this is a request for a "custom view".
 */
if:x:/@camphora._internal.get-app-url-entities/*?count
  >:int:1
  and:x:/@camphora._internal.get-app-url-entities/1?name
    !=:download
  and:x:/@camphora._internal.get-app-url-entities/1?name
    !=:upload
  and:x:/@camphora._internal.get-app-url-entities/1?name
    !=:tag

  /*
   * Probably a request for a "view", making sure the view actually exists, before 
   * we evaluate it, and return early.
   */
  if
    fetch:x:/0/0?value
      file-exists:/modules/{0}/{1}.hl
        :x:/@camphora._internal.get-app-url-entities/0?name
        :x:/@camphora._internal.get-app-url-entities/1?name

    /*
     * View exists, evaluating its associated file, and returning early, 
     * to avoid evaluation of the rest of our lambda.
     */
    micro.evaluate.file:/modules/{0}/{1}.hl
      :x:/@camphora._internal.get-app-url-entities/0?name
      :x:/@camphora._internal.get-app-url-entities/1?name
    return

  else

    /*
     * View didn't exist, returning 404 to client.
     */
    p5.web.response.set-status-code:404
    p5.web.echo:This is where URLs go to die
    return





/*
 * Creating main content container.
 */
create-container-widget:camphora-main-container
  class:container





/*
 * Changing the title of page.
 */
p5.web.page.set-title:{0}
  :x:/@camphora._internal.get-app-url-entities/0?name





/*
 * Including Micro, adding Awesome Fonts, adding app's chosen skin, in addition
 * to adding our main CSS file for currently evaluated Camphora app.
 */
eval-x:x:/+/*
micro.css.include
  skin:x:/@load-file/*/*/skin?value
p5.web.include-css-file:/modules/{0}/media/main.css
  :x:/@camphora._internal.get-app-url-entities/0?name





/*
 * Verifying user is allowed to view data, and if not, forcing him to login,
 * and returning early, to avoid evaluating the rest of our file.
 */
whoami
if:x:/@whoami/*/role?value
  !=:root
  and:x:/@load-file/*/*/viewers?value
    !=:guest
  and:x:/@load-file/*/*/viewers?value
    !=:x:/@whoami/*/role?value

  /*
   * Forcing user to log in, and returning early.
   */
  p5.core.login
  return





/*
 * Checking if current request is trying to download our temp CSV file, and if so,
 * returning it to the client as is.
 */
if:x:/@camphora._internal.get-app-url-entities/*?count
  =:int:2
  and:x:/@camphora._internal.get-app-url-entities/1?name
    =:download

  /*
   * Current request is trying to download the temporary created CSV file.
   *
   * Making sure we get our HTTP headers set correctly, and echoing file as is.
   */
  p5.web.header.set:Content-Type
    src:text/csv
  p5.web.header.set:Content-Disposition
    src:@"attachment; filename=""{0}.csv"""
      :x:/@camphora._internal.get-app-url-entities/0?name
  p5.web.echo-file:~/temp/{0}.csv
    :x:/@camphora._internal.get-app-url-entities/0?name

  /*
   * Returning early to abort evaluation of the rest of our file.
   */
  return





/*
 * Iterating through each field in app, adding one column for each field that is
 * supposed to be visible in the grid - But only if settings for app has told us to
 * actually show any headers at all.
 *
 * Notice, we're tracking the first textarea type of column, and making it "100%" wide, 
 * which will prioritize the first column in regards to spacing, and make all other columns
 * reduce accordingly in size, to have no more width than what's absolutely necessary
 * in order to show their values, without breaking text into multiple lines.
 */
if:x:/@load-file/*/*/show-headers?value

  /*
   * Tracking first header of type [textarea], since we want to make sure it have
   * priority in spacing, by making it 100% width, which will force it to become the 
   * largest column in our datagrid.
   */
  _first:bool:true

  /*
   * Looping through each [field] declaration in "app-manifest.hl" file for app.
   */
  for-each:x:/@load-file/*/*/field

    /*
     * Checking if field is supposed to be visible in grid.
     */
    if:x:/@_dp/#/*/show?value
      =:bool:false

      /*
       * Current field is not supposed to be visible in the datagrid.
       */
      continue

    /*
     * Checking if this is a "checked" type of field.
     */
    if:x:/@_dp/#/*/type?value
      =:checkbox

      /*
       * Yup, checkbox column type - Reducing width of column.
       *
       * This little CSS trick will make the column no wider than what it absolutely
       * needs to be, to show its header (th) cell.
       */
      add:x:/..for-each/*/add/*/*
        src
          style:"width:5px;"

    else-if:x:/@_first?value
      and:x:/@_dp/#/*/type?value
        =:textarea

      /*
       * This is our first text or textarea column, making it 100% wide, to throw
       * our CSS trick from above (read comments above).
       */
      add:x:/..for-each/*/add/*/*
        src
          style:"width:100%;"

    /*
     * Adding a column to our datagrid below.
     */
    set:x:/+2/*/*?name
      src:x:/@_dp/#/*/name?value
    eval-x:x:/+/*/*/*/*/*/innerValue
    add:x:/../*/create-widget/**/micro.widgets.grid/*/columns
      src
        foo
          class:capitalize
          widgets
            a
              href:#
              role:button
              innerValue:x:/@_dp/#/*/name?value
              onclick

                /*
                 * Forward evaluated above.
                 */
                _app-name:x:/@camphora._internal.get-app-url-entities/0?name

                /*
                 * Storing sort column in ViewState and re-databinding datagrid.
                 */
                get-widget-property:x:/../*/_event?value
                  innerValue
                p5.web.viewstate.set:camphora.sort-column
                  src:x:/@get-widget-property/*/*?value

                /*
                 * Making sure we update CSS class of bugger.
                 */
                p5.web.widgets.find-like
                  class:camphora-sort-column
                if:x:/-/*/*?value
                  !=:x:/../*/_event?value

                  /*
                   * User selected a different sorting column.
                   */
                  micro.css.delete:x:/@p5.web.widgets.find-like/*/*?value
                    class:camphora-sort-column camphora-sort-column-asc camphora-sort-column-desc
                  micro.css.add:x:/../*/_event?value
                    class:camphora-sort-column

                /*
                 * Checking if we should sort ascending or descending.
                 */
                get-widget-property:x:/../*/_event?value
                  class
                if:x:/@get-widget-property/*/*?value
                  ~:camphora-sort-column-asc

                  /*
                   * Grid was sorted ascendingly, toggling it to become descendingly sorted.
                   */
                  micro.css.toggle:x:/../*/_event?value
                    class:camphora-sort-column-asc camphora-sort-column-desc
                  p5.web.viewstate.set:camphora.sort-column-direction
                    src:desc

                else-if:x:/@get-widget-property/*/*?value
                  ~:camphora-sort-column-desc

                  /*
                   * Grid was sorted descendingly, toggling it to become ascendingly sorted.
                   */
                  micro.css.toggle:x:/../*/_event?value
                    class:camphora-sort-column-asc camphora-sort-column-desc
                  p5.web.viewstate.set:camphora.sort-column-direction
                    src:asc

                else

                  /*
                   * Grid was not sorted by this column at all, defaulting to ascendingly.
                   */
                  p5.web.viewstate.set:camphora.sort-column-direction
                    src:asc
                  micro.css.add:x:/../*/_event?value
                    class:camphora-sort-column-asc

                /*
                 * Re-databinding grid, now with an explicit sort order column
                 * in ViewState.
                 */
                micro.evaluate.file:/modules/{0}/helpers/databind-grid.hl
                  :x:/@_app-name?value





/*
 * Verifying user is allowed to edit data, and if not, removing edit widgets
 * from our form.
 */
whoami
if:x:/@whoami/*/role?value
  !=:root
  and:x:/@load-file/*/*/editors?value
    !=:guest
  and:x:/@load-file/*/*/editors?value
    !=:x:/whoami/*/role?value

  /*
   * Making sure all other parts of our system knows that user is NOT allowed 
   * to edit data in the system.
   */
  p5.web.viewstate.set:camphora.editing-allowed
    src:bool:false

  /*
   * Removing the "add new record button".
   */
  set:x:/../*/create-widget/**/button/=camphora-add-new-record-button

  /*
   * Removing the "import CSV file button".
   */
  set:x:/../*/create-widget/**/button/=camphora-import-csv-file

  /*
   * Removing the "delete all button".
   */
  set:x:/../*/create-widget/**/button/=camphora-delete-all-button

  /*
   * Removing obscurer, since it's only used when files are uploaded.
   */
  set:x:/../*/create-widget/**/micro.widgets.obscurer/=camphora-upload-obscurer

else

  /*
   * Checking if current request is trying to upload a CSV file, and if so,
   * invoking file responsible for handling our upload, and returning early.
   */
  if:x:/@camphora._internal.get-app-url-entities/*?count
    =:int:2
    and:x:/@camphora._internal.get-app-url-entities/1?name
      =:upload

    /*
     * Evaluating file responsible for handling our upload, and returning early,
     * to avoid evaluation of the rest of our file.
     *
     * Making sure we wrap our invocation in a try/catch block.
     */
    try

      /*
       * Evaluating "upload" file.
       */
      eval-x:x:/+/*/app-name
      micro.evaluate.file:/modules/{0}/helpers/upload-file.hl
        :x:/@camphora._internal.get-app-url-entities/0?name
        app-name:x:/@camphora._internal.get-app-url-entities/0?name

    catch

      /*
       * Making sure we return "error" to client.
       */
      p5.web.echo:ERROR

    /*
     * Returning early to avoid evaluation of the rest of our file.
     */
    return

  /*
   * Creating Edit row header, but only if headers are supposed to be shown.
   */
  if:x:/@load-file/*/*/show-headers?value

    /*
     * Headers are visible for app, adding the "Edit" header.
     */
    add:x:/../*/create-widget/**/micro.widgets.grid/*/columns
      src
        Edit
          style:"width:5px;text-align:right;"

  /*
   * Making sure all other parts of our system knows the user is allowed to edit data
   * in the system.
   */
  p5.web.viewstate.set:camphora.editing-allowed
    src:bool:true

  /*
   * Making sure we create a CSV file dropzone for our page.
   */
  eval-x:x:/+/*/url
  micro.page.create-dropzone
    filter:csv
    url:{0}/upload
      :x:/../*/camphora._internal.get-app-url-entities/[0,1]/0?name
    .onfinish:@"function(uid, no_files, no_errors) {if(no_errors > 0){alert('There were errors when trying to import your file');p5.$('camphora-upload-obscurer').el.style.display = 'none';} else {window.location.replace(window.location.href);}}"
    .onbegin:@"function() {p5.$('camphora-upload-obscurer').el.style.display = 'block';}"





/*
 * Figuring out if a query search parameter was supplied.
 */
p5.web.query.get:q
if:x:/@p5.web.query.get/*?value

  /*
   * Query parameter was given.
   */
  eval-x:x:/+/*/*/value
  add:x:/../*/create-widget/**/input/=camphora-search-query
    src
      value:x:/@p5.web.query.get/*?value
  add:x:/../*/create-widget/**/input/=camphora-search-query/*/oninit
    src
      micro.page.set-focus:x:/../*/_event?value





/*
 * Checking if we have alternative views, and if so, making sure we display our
 * "view views" button.
 */
if:x:/@load-file/*/*/views/*?count
  >:int:0

  /*
   * There are alternative views, making sure we inject our "view views" button.
   *
   * First injecting a datagrid item for each view in app.
   */
  for-each:x:/@load-file/*/*/views/*?name
    eval-x:x:/+/*/*/*/*/*/*
    add:x:/..if/*/insert-after/**/micro.widgets.grid/*/rows
      src
        item
          View
            widgets
              a
                innerValue:x:/@_dp?value
                style:"display:block;"
                href:/{0}/{1}
                  :x:/@camphora._internal.get-app-url-entities/0?name
                  :x:/@_dp?value

  /*
   * Then inserting our open modal window button, that allows the user to select
   * a specific view.
   */
  insert-after:x:/../*/create-widget/**/widgets/=toolbar/0
    src
      button
        innerValue:@"<span class=""icon-eye""></span>"
        title:View alternative views
        onclick

          /*
           * Showing a modal window, allowing user to open up alternative views for app.
           */
          create-widgets
            micro.widgets.modal:camphora-view-views
              widgets
                h3
                  innerValue:Alternative views
                micro.widgets.grid
                  class:hover striped
                  rows
                div
                  class:right
                  widgets
                    button
                      innerValue:Close
                      title:Close window
                      oninit

                        /*
                         * Setting initial focus to "close" button.
                         */
                        micro.page.set-focus:x:/../*/_event?value

                      onclick

                        /*
                         * Closing "view views" modal window.
                         */
                        delete-widget:camphora-view-views
  





/*
 * Creating main wire-frame grid.
 */
eval-x:x:/+/**(/_app-name)
create-widget
  parent:camphora-main-container
  widgets

    /*
     * Obscurer show when files are uploaded.
     */
    micro.widgets.obscurer:camphora-feed-more-obscurer
      style:"display:none;"
      message:"Please wait while we fetch more items ..."

    /*
     * Obscurer show when files are uploaded.
     */
    micro.widgets.obscurer:camphora-upload-obscurer
      style:"display:none;"
      message:"Please wait while we upload and import your file(s) ..."

    div
      class:row
      widgets
        div
          class:col-100
          oninit

            /*
             * Including JavaScript to determine is user is at the bottom of page,
             * at which point we "auto-feed" grid with more items.
             */
            p5.web.include-javascript:@"
window.onscroll = function() {
  if ((window.innerHeight + window.scrollY) >= document.body.offsetHeight) {
    if (!p5.camphora_end_of_dataset) {
      if(p5.camphora_ongoing_scroll) {
        return;
      }
      p5.camphora_ongoing_scroll = true;
      var obsc = p5.$('camphora-feed-more-obscurer').el;
      obsc.style.display = 'block';
      var el = p5.$('camphora-crud-grid');
      el.raise('.onfeed',{
        onsuccess:function() {
          p5.camphora_ongoing_scroll = false;
          obsc.style.display = 'none';
        }
      });
    }
  }
};"
          widgets
            div
              class:row
              widgets
                div
                  class:col-50
                  widgets
                    div
                      class:strip fill
                      widgets
                        input:camphora-search-query
                          type:text
                          placeholder:Search ...
                          onkeydown:@"if (event.keyCode == 13) {p5.$('camphora-search-button').raise('onclick');return false;}"
                          oninit

                            /*
                             * Forward evaluated above.
                             */
                            _app-name:x:/@camphora._internal.get-app-url-entities/0?name

                            /*
                             * Modifying placeholder to contain record count.
                             */
                            p5.mysql.connect:[camphora]
                              p5.mysql.scalar:@"select count(*) from `{0}`"
                                :x:/@_app-name?value
                              set-widget-property:x:/../*/_event?value
                                placeholder:Search amongst {0} records ...
                                  :x:/@p5.mysql.scalar?value

                            /*
                             * Setting focus initially to search textbox.
                             */
                            micro.page.set-focus:x:/../*/_event?value

                        button:camphora-search-button
                          innerValue:@"<span class=""icon-search""></span>"
                          title:Search for items
                          onclick

                            /*
                             * Forward evaluated above.
                             */
                            _app-name:x:/@camphora._internal.get-app-url-entities/0?name

                            /*
                             * Figuring out filter, setting it to viewstate,
                             * and re-databinding grid.
                             */
                            get-widget-property:camphora-search-query
                              value
                            if:x:/@get-widget-property/*/*?value
                              !=:

                              /*
                               * Filter was given, storing it in ViewState before re-databinding grid,
                               * and making sure we enable our "clear filter button".
                               */
                              p5.web.viewstate.set:camphora.filter
                                src:x:/@get-widget-property/*/*?value
                              delete-widget-property:camphora-remove-all-filters
                                disabled

                            else

                              /*
                               * Filter was NOT given, removing it from ViewState before re-databinding grid,
                               * and making sure we disable our "clear filter button".
                               */
                              p5.web.viewstate.set:camphora.filter
                              set-widget-property:camphora-remove-all-filters
                                disabled

                            /*
                             * Re-databinding grid.
                             */
                            micro.evaluate.file:/modules/{0}/helpers/databind-grid.hl
                              :x:/@_app-name?value

                            /*
                             * Selecting textbox content, and setting focus to it.
                             */
                            micro.page.set-focus:camphora-search-query

                        button:camphora-remove-all-filters
                          innerValue:@"<span class=""icon-cross""></span>"
                          title:Remove all search and tag filters
                          disabled
                          oninit

                            /*
                             * Checking if current URL is a request for items with
                             * a specific "hash tag", at which point we enable the clear button.
                             */
                            camphora._internal.get-app-url-entities
                            if:x:/@camphora._internal.get-app-url-entities/1?name
                              =:tag

                              /*
                               * Current request is a request for a specific tag, hence
                               * we enable the "clear filter" button, to allow user to
                               * clear the tag, and go to "root" URL of app.
                               */
                              delete-widget-property:x:/../*/_event?value
                                disabled

                          onclick

                            /*
                             * Forward evaluated above.
                             */
                            _app-name:x:/@camphora._internal.get-app-url-entities/0?name

                            /*
                             * To clear out any viewstate and tags, we simply re-direct
                             * the client to base URL of app.
                             */
                            p5.web.set-location:x:/@_app-name?value

                div
                  class:col-50
                  widgets
                    div
                      class:strip toolbar
                      style:"float:right;"
                      widgets:toolbar
                        button:camphora-add-new-record-button
                          innerValue:@"<span class=""icon-plus""></span>"
                          class:larger
                          title:Create new record
                          onclick

                            /*
                             * Forward evaluated above.
                             */
                            _app-name:x:/@camphora._internal.get-app-url-entities/0?name

                            /*
                             * Making sure user is not editing another item, since this will
                             * raise an exception, due to static IDs in widgets.
                             */
                            p5.web.widgets.find
                              .edit
                            if:x:/@p5.web.widgets.find/*/*?value

                              /*
                               * Warning use that he needs to finish up editing existing item,
                               * before he can create new items.
                               */
                              micro.windows.info:You have to finish editing your item, before you can create a new item
                                class:micro-windows-info warning
                              return

                            /*
                             * Evaluating file responsible for creating a new record.
                             */
                            micro.evaluate.file:/modules/{0}/helpers/create-item.hl
                              :x:/@_app-name?value

                        button
                          innerValue:@"<span class=""icon-download2""></span>"
                          title:Download items as CSV file
                          onclick

                            /*
                             * Forward evaluated above.
                             */
                            _app-name:x:/@camphora._internal.get-app-url-entities/0?name

                            /*
                             * Evaluating file responsible for exporting items to client.
                             */
                            eval-x:x:/+/*/app-name
                            micro.evaluate.file:/modules/{0}/helpers/export-items.hl
                              :x:/@_app-name?value
                              app-name:x:/@_app-name?value

                        button:camphora-import-csv-file
                          innerValue:@"<span class=""icon-upload2""></span>"
                          title:Import a CSV file from your local disc
                          onclick:@"p5.dropzone.browse();event.stopPropagation(true);return false;"

                        button:camphora-delete-all-button
                          innerValue:@"<span class=""icon-bin""></span>"
                          title:Deletes all items in your database
                          onclick

                            /*
                             * Forward evaluated above.
                             */
                            _app-name:x:/@camphora._internal.get-app-url-entities/0?name

                            /*
                             * Asking user to confirm action.
                             */
                            eval-x:x:/+/*/*/*/p/*/innerValue
                            create-widgets
                              micro.widgets.modal:camphora-delete-all-records
                                widgets
                                  h3
                                    innerValue:Please confirm action
                                  p
                                    innerValue:These will delete all records in your database of type '{0}', are you sure you wish to continue?
                                      :x:/@_app-name?value
                                  div
                                    class:right
                                    widgets
                                      div
                                        class:strip
                                        widgets
                                          button
                                            innerValue:Yes
                                            oninit

                                              /*
                                               * Setting initial focus to "Yes" button.
                                               */
                                              micro.page.set-focus:x:/../*/_event?value

                                            onclick

                                              /*
                                               * Forward evaluated above.
                                               */
                                              _app-name:x:/@camphora._internal.get-app-url-entities/0?name

                                              /*
                                               * Deleting all records from database.
                                               */
                                              p5.mysql.connect:[camphora]
                                                p5.mysql.delete:@"delete from `{0}`"
                                                  :x:/@_app-name?value

                                              /*
                                               * Reloading root location for simplicity.
                                               */
                                              p5.web.set-location:/{0}
                                                :x:/@_app-name?value

                                          button
                                            innerValue:No
                                            onclick

                                              /*
                                               * Deleting modal widget.
                                               */
                                              delete-widget:camphora-delete-all-records

                        button
                          innerValue:@"<span class=""icon-home3""></span>"
                          title:Return to home
                          onclick

                            /*
                             * Redirecting user to server's root URL.
                             */
                            p5.web.get-root-location
                            p5.web.set-location:x:/-?value

    div
      class:row
      widgets
        div
          class:col-100
          widgets
            div
              class:camphora-crud-grid-wrapper
              widgets
                micro.widgets.grid:camphora-crud-grid
                  class:striped hover
                  .onfeed

                    /*
                     * Feeding grid with more items.
                     */
                    _app-name:x:/@camphora._internal.get-app-url-entities/0?name
                    micro.evaluate.file:/modules/{0}/helpers/databind-grid.hl
                      :x:/@_app-name?value
                      feed-more:bool:true

                  columns
                  oninit

                    /*
                     * Forward evaluated above.
                     */
                    _app-name:x:/@camphora._internal.get-app-url-entities/0?name

                    /*
                     * Evaluating file responsible for databinding grid.
                     */
                    eval-x:x:/+/*/url
                    micro.evaluate.file:/modules/{0}/helpers/databind-grid.hl
                      :x:/@_app-name?value
                      url:x:/@_app-name?value
