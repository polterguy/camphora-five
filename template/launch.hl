/*
 * Launcher file for Camphora Five generated CRUD app.
 */





/*
 * This is your app's default view.
 * Exchange this if you'd like for your app to use another
 * view as its "default view".
 *
 * The default view is the view that is evaluated at your app's root URL.
 */
.default-view:default.hl





/*
 * Retrieves URL entities to see if client is requesting a custom "view".
 */
.view
micro.url.get-entities





/*
 * Checking if this is a request for a "custom view".
 *
 * Notice, this is true if the current request's URL is
 * either consisting of two or more entities (folders), or the
 * URL is consisting of one entity, and that single entity
 * does not point to the name of the app. The latter implies
 * that this Camphora Five app is configured as the default
 * app in web.config, and the user is requesting a URL within
 * it.
 */
if:x:/@micro.url.get-entities/*?count
  >:int:1

  /*
   * Definitely a view request.
   */
  set:x:/@.view?value
    src:x:/@micro.url.get-entities/1?name

else-if:x:/@micro.url.get-entities/*?count
  =:int:1
  and:x:/@micro.url.get-entities/0?name
    !=:{app-name}

  /*
   * This might happen if the current Camphora app is the "default app" declared
   * in the server's web.config file.
   */
  set:x:/@.view?value
    src:x:/@micro.url.get-entities/0?name





/*
 * Checking if client was requesting a view.
 */
if:x:/@.view?value

  /*
   * Probably a request for a "view", making sure the view actually exists, before
   * we evaluate it.
   */
  if
    fetch:x:/0/0?value
      file-exists:/modules/{0}/views/{1}.hl
        :{app-name}
        :x:/@.view?value

    /*
     * View exists, evaluating its associated file, and returning early,
     * to avoid evaluation of the rest of our lambda.
     */
    micro.evaluate.file:/modules/{0}/views/{1}.hl
      :{app-name}
      :x:/@.view?value

  else

    /*
     * View didn't exist, returning 404 to client.
     */
    p5.web.response.set-status-code:404
    p5.web.echo:This is where URLs go to die

else

  /*
   * A normal request, evaluating default view.
   */
  micro.evaluate.file:/modules/{0}/views/{1}
    :{app-name}
    :x:/../*/.default-view?value
