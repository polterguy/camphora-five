
/*
 * Launcher file for Camphora Five generated app.
 */





/*
 * Including Micro, adding Awesome Fonts, and serious skin.
 */
p5.web.include-css-file:@MICRO/media/main.css
p5.web.include-css-file:@MICRO/media/ext.css
p5.web.include-css-file:@MICRO/media/fonts.css
p5.web.include-css-file:@MICRO/media/skins/serious.css
p5.web.include-css-file:/modules{0}/media/main.css
  :x:/../*/url?value





/*
 * Loading template file, and creating one column in our datagrid for each [field]
 * in our "app.hl" file.
 *
 * Notice, we're tracking the first column, and making it "100%" wide, which will
 * prioritize the first column in regards to spacing, and make all other columns
 * reduce accordingly in size, to have no more width than what's absolutely necessary
 * in order to show their values, without breaking.
 */
_first:bool:true
load-file:/modules{0}/helpers/app.hl
  :x:/../*/url?value
for-each:x:/@load-file/*/*/field

  /*
   * Checking if field is supposed to be visible in grid.
   */
  if:x:/@_dp/#/*/show?value
    =:bool:false

    /*
     * Current field is not supposed to be visible in the datagrid.
     */
    continue

  /*
   * Checking if this is a "checked" type of field.
   */
  if:x:/@_dp/#/*/type?value
    =:checkbox

    /*
     * Yup, checked - Reducing width of column.
     *
     * This little CSS trick will make the column no wider than what it absolutely
     * needs to be, to show its header (th) cell.
     */
    add:x:/..for-each/*/add/*/*
      src
        style:"width:5px;"

  else-if:x:/@_first?value

    /*
     * This is our first "non-checked" column, making it 100% wide, to throw
     * our CSS trick from above (read comments above).
     */
    add:x:/..for-each/*/add/*/*
      src
        style:"width:100%;"

  /*
   * Adding a column to our datagrid below.
   */
  set:x:/+/*/*?name
    src:x:/@_dp/#/*/name?value
  add:x:/../*/create-widget/**/micro.widgets.grid/*/columns
    src
      foo
        class:capitalize





/*
 * Creating delete row header
 */
add:x:/../*/create-widget/**/micro.widgets.grid/*/columns
  src
    Delete
      style:"width:5px;"





/*
 * Creating main wire-frame grid.
 */
set-widget-property:cnt
  class:container
eval-x:x:/+/**(/_folder|/_url)
create-widget
  class:container
  widgets
    div
      class:row
      widgets
        div
          class:col-100
          widgets
            div
              class:right
              widgets
                div
                  class:strip
                  style:"display:inline-block;"
                  widgets
                    button
                      innerValue:@"<span class=""icon-plus""></span>"
                      style:"padding-left:35px;padding-right:35px;"
                      onclick

                        /*
                         * Forward evaluated above.
                         */
                        _url:x:/../*/url?value

                        /*
                         * Evaluating file responsible for creating a new record.
                         */
                        micro.evaluate.file:/modules{0}/helpers/create-item.hl
                          :x:/@_url?value

                    button
                      innerValue:@"<span class=""icon-home""></span>"
                      onclick

                        /*
                         * Redirecting user to server's root URL.
                         */
                        p5.web.get-root-location
                        p5.web.set-location:x:/-?value

    div
      class:row
      widgets
        div
          class:col-100
          widgets
            div
              class:air-inner shaded rounded
              widgets
                micro.widgets.grid:camphora-crud-grid
                  class:hover striped spaced lg-headers
                  columns
                  oninit

                    /*
                     * Forward evaluated above.
                     */
                    _folder:/modules{0}/
                      :x:/../*/url?value

                    /*
                     * Evaluating file responsible for databinding grid.
                     */
                    micro.evaluate.file:{0}helpers/databind-grid.hl
                      :x:/@_folder?value
