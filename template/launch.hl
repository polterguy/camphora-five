
/*
 * Launcher file for Camphora Five generated app.
 */





/*
 * Making sure we set the CSS class for our main wrapper widget correctly.
 */
set-widget-property:cnt
  class:container





/*
 * Splitting URL, to figure out which type of request this is, and to get the
 * inclusion of our CSS file(s) correctly.
 */
split:x:/../*/url?value
  =:/





/*
 * Including Micro, adding Awesome Fonts, and serious skin.
 */
p5.web.include-css-file:@MICRO/media/main.css
p5.web.include-css-file:@MICRO/media/ext.css
p5.web.include-css-file:@MICRO/media/fonts.css
p5.web.include-css-file:@MICRO/media/skins/serious.css
p5.web.include-css-file:/modules/{0}/media/main.css
  :x:/@split/0?name





/*
 * Loading file containing app declaration.
 */
load-file:/modules/{0}/helpers/app-manifest.hl
  :x:/@split/0?name





/*
 * Verifying user is allowed to view data, and if not, forcing him to login,
 * and returning early, to avoid evaluating the rest of our file.
 */
whoami
if:x:/@whoami/*/role?value
  !=:root
  and:x:/@load-file/*/*/viewers?value
    !=:guest
  and:x:/@load-file/*/*/viewers?value
    !=:x:/whoami/*/role?value

  /*
   * Forcing user to log in, and returning early.
   */
  p5.core.login
  return





/*
 * Iterating through each field in app, adding one column for each field that is
 * supposed to be visible in the grid.
 *
 * Notice, we're tracking the first textarea type of column, and making it "100%" wide, 
 * which will prioritize the first column in regards to spacing, and make all other columns
 * reduce accordingly in size, to have no more width than what's absolutely necessary
 * in order to show their values, without breaking text into multiple lines.
 */
_first:bool:true
for-each:x:/@load-file/*/*/field

  /*
   * Checking if field is supposed to be visible in grid.
   */
  if:x:/@_dp/#/*/show?value
    =:bool:false

    /*
     * Current field is not supposed to be visible in the datagrid.
     */
    continue

  /*
   * Checking if this is a "checked" type of field.
   */
  if:x:/@_dp/#/*/type?value
    =:checkbox

    /*
     * Yup, checked - Reducing width of column.
     *
     * This little CSS trick will make the column no wider than what it absolutely
     * needs to be, to show its header (th) cell.
     */
    add:x:/..for-each/*/add/*/*
      src
        style:"width:5px;"

  else-if:x:/@_first?value
    and:x:/@_dp/#/*/type?value
      =:textarea

    /*
     * This is our first text or textarea column, making it 100% wide, to throw
     * our CSS trick from above (read comments above).
     */
    add:x:/..for-each/*/add/*/*
      src
        style:"width:100%;"

  /*
   * Adding a column to our datagrid below.
   */
  set:x:/+/*/*?name
    src:x:/@_dp/#/*/name?value
  add:x:/../*/create-widget/**/micro.widgets.grid/*/columns
    src
      foo
        class:capitalize





/*
 * Verifying user is allowed to edit data, and if not, removing edit widgets
 * from our form.
 */
whoami
if:x:/@whoami/*/role?value
  !=:root
  and:x:/@load-file/*/*/editors?value
    !=:guest
  and:x:/@load-file/*/*/editors?value
    !=:x:/whoami/*/role?value

  /*
   * Making sure all other parts of our system knows the user is not allowed to edit data
   * in the system.
   */
  p5.web.viewstate.set:camphora.editing-allowed
    src:bool:false

else

  /*
   * Creating Edit row header
   */
  add:x:/../*/create-widget/**/micro.widgets.grid/*/columns
    src
      Edit
        style:"width:5px;text-align:center;"

  /*
   * Making sure all other parts of our system knows the user is allowed to edit data
   * in the system.
   */
  p5.web.viewstate.set:camphora.editing-allowed
    src:bool:true





/*
 * Creating main wire-frame grid.
 */
eval-x:x:/+/**(/_folder|/_url)
create-widget
  class:container
  widgets
    div
      class:row
      widgets
        div
          class:col-100
          widgets
            div
              class:right
              style:"min-width:450px;"
              widgets
                div
                  class:strip
                  style:"display:inline-block;"
                  widgets
                    input:camphora-search-query
                      type:text
                      placeholder:Search ...
                      onkeydown:@"if (event.keyCode == 13) {p5.$('camphora-search-button').raise('onclick');return false;}"

                    button:camphora-search-button
                      innerValue:@"<span class=""icon-search""></span>"
                      title:Search for items
                      onclick

                        /*
                         * Forward evaluated above.
                         */
                        _url:x:/@split/0?name

                        /*
                         * Databinding grid, now with [filter].
                         */
                        get-widget-property:camphora-search-query
                          value
                        if:x:/@get-widget-property/*/*?value
                          !=:

                          /*
                           * Filter was given.
                           */
                          eval-x:x:/+/*/filter
                          micro.evaluate.file:/modules/{0}/helpers/databind-grid.hl
                            :x:/@_url?value
                            filter:x:/@get-widget-property/*/*?value

                        else

                          /*
                           * Filter was removed.
                           */
                          micro.evaluate.file:/modules/{0}/helpers/databind-grid.hl
                            :x:/@_url?value

                        /*
                         * Setting focus to search textbox for simplicity.
                         */
                        micro.page.set-focus:camphora-search-query

                    button
                      innerValue:@"<span class=""icon-plus""></span>"
                      style:"padding-left:35px;padding-right:35px;"
                      title:Create new record
                      onclick

                        /*
                         * Forward evaluated above.
                         */
                        _url:x:/@split/0?name

                        /*
                         * Evaluating file responsible for creating a new record.
                         */
                        micro.evaluate.file:/modules/{0}/helpers/create-item.hl
                          :x:/@_url?value

                    button
                      innerValue:@"<span class=""icon-download""></span>"
                      title:Download items as CSV file
                      onclick

                    button
                      innerValue:@"<span class=""icon-home""></span>"
                      title:Return to home
                      onclick

                        /*
                         * Redirecting user to server's root URL.
                         */
                        p5.web.get-root-location
                        p5.web.set-location:x:/-?value

    div
      class:row
      widgets
        div
          class:col-100
          widgets
            div
              class:camphora-crud-grid-wrapper
              widgets
                micro.widgets.grid:camphora-crud-grid
                  class:spaced lg-headers hover
                  columns
                  oninit

                    /*
                     * Forward evaluated above.
                     */
                    _url:x:/../*/url?value
                    _folder:/modules/{0}/
                      :x:/@split/0?name

                    /*
                     * Evaluating file responsible for databinding grid.
                     */
                    eval-x:x:/+/*/url
                    micro.evaluate.file:{0}helpers/databind-grid.hl
                      :x:/@_folder?value
                      url:x:/@_url?value
