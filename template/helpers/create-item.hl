/*
 * This is a _"template file"_, implying this file will be copied into
 * your Camphora Five generated app's folder when your app is generated,
 * and possibly modified to reflect your app's name/structure.
 *
 * File responsible for creating a new item for the specified __[datagrid]__ datagrid.
 *
 * Displays a modal widget with an input field for all fields in app
 * that user can supply.
 */





/*
 * Sanity checking invocation
 */
micro.lambda.contract.min:x:/..
  datagrid:string





/*
 * Figuring out path of app's "/helpers/" folder.
 */
micro.path.get-folder:x:/..?name





/*
 * Creating a modal widget, asking user to supply his data for his new record.
 */
eval-x:x:/+/**/.datagrid
create-widgets
  micro.widgets.modal:camphora-new-item-modal
    widgets

      /*
       * Some generic header.
       */
      h3
        innerValue:Data for your '{0}' record
          :{app-name}

      /*
       * Widget wrapping our actual column values.
       */
      div
        widgets:data

      /*
       * Button strip for "Save" and "Cancel" buttons.
       */
      div
        class:right strip
        widgets

          /*
           * Save new record button.
           */
          button:camphora-save-record-button
            innerValue:Save
            onclick

              /*
               * Serializing form's data.
               */
              .datagrid:x:/../*/datagrid?value
              micro.form.serialize:camphora-new-item-modal
              add:x:/+
                src:x:/@micro.form.serialize/*
              camphora.database.insert:x:/@.datagrid?value

              /*
               * Deleting modal widget.
               */
              delete-widget:camphora-new-item-modal

              /*
               * Re-databinding datagrid, making sure we remove filter, page,
               * and order parameters.
               */
              camphora.datagrid.databind:x:/@.datagrid?value
                filter
                page
                order-by
                order-dir

              /*
               * Resets the CSS classes of all "sort header linkbuttons".
               */
              p5.web.widgets.find-like:x:/@.datagrid?value
                class:camphora-sort-column
              micro.css.delete:x:/@p5.web.widgets.find-like/*/*?value
                class:camphora-sort-column-asc camphora-sort-column-desc

              /*
               * Resetting filter.
               */
              p5.web.widgets.find-first
                .toolbar:x:/@.datagrid?value
              p5.web.widgets.find-first:x:/-/*/*?value
                .filter
              set-widget-property:x:/-/*/*?value
                value:

          button
            innerValue:Cancel
            onclick

              /*
               * Simply closing modal widget.
               */
              delete-widget:camphora-new-item-modal
