/*
 * File responsible for creating a new item.
 *
 * Displays a modal widget with an input field for all fields in app
 * that user can supply.
 */





/*
 * Figuring out path of app's "/helpers/" folder.
 */
micro.path.get-folder:x:/..?name





/*
 * Creating a modal widget, asking user to supply his data for his new record.
 */
create-widgets
  micro.widgets.modal:camphora-new-item-modal
    widgets

      /*
       * Some generic header.
       */
      h3
        innerValue:Data for your '{0}' record
          :{app-name}

      /*
       * Widget wrapping our actual column values.
       */
      div
        widgets:data

      /*
       * Button strip for "Save" and "Cancel" buttons.
       */
      div
        class:right strip
        widgets

          /*
           * Save new record button.
           */
          button:camphora-save-record-button
            innerValue:Save
            onclick

              /*
               * Serializing form's data.
               */
              micro.form.serialize:camphora-new-item-modal
              add:x:/+
                src:x:/@micro.form.serialize/*
              camphora.database.insert

              /*
               * Deleting modal widget.
               */
              delete-widget:camphora-new-item-modal

              /*
               * Re-databinding datagrid, making sure we remove filter, page,
               * and order parameters.
               */
              camphora.datagrid.databind
                filter
                page
                order-by
                order-dir

              /*
               * Updates count.
               */
              camphora.count-changed

          button
            innerValue:Cancel
            onclick

              /*
               * Simply closing modal widget.
               */
              delete-widget:camphora-new-item-modal
