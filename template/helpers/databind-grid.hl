/*
 * This file will databind the specified __[datagrid]__ camphora datagrid.
 *
 * Optionally pass in the following arguments.
 *
 * - __[filter]__ - Filter to use for databind operation.
 * - __[page]__ - Page to view.
 * - __[order-by]__ - Column to sort according to.
 * - __[order-dir]__ - Sorting direction, which might be _"asc"_ or _"desc"_.
 */





/*
 * Separating arguments from the rest of our lambda.
 */
.signal





/*
 * Sanity checking invocation.
 */
micro.lambda.contract.min:x:/..
  datagrid:string





/*
 * Sanity checking invocation.
 */
micro.lambda.contract.optional:x:/..
  filter
  page
  order-by
  order-dir





/*
 * Setting properties, if given.
 */
for-each:x:/@.signal/--(/filter|/page|/order-by|/order-dir)

  /*
   * Changing name of [foo] node below to property event name.
   */
  set:x:/..for-each/*/foo?name
    src:camphora.{0}.datagrid.{1}
      :{app-name}
      :x:/@_dp/#?name

  /*
   * The below [foo] node is actually our property setter invocation,
   * but its name is changed above, before we invoke it.
   *
   * Before we can invoke it though, we'll need to apply its arguments.
   */
  add:x:/+2/*
    src:x:/@_dp/#/*
  eval-x:x:/+/*
  foo:x:/../*/datagrid?value
    value:x:/@_dp/#?value





/*
 * Retrieving properties from persistent property events.
 */
.properties
  camphora.datagrid.filter
  camphora.datagrid.page
  camphora.datagrid.order-by
  camphora.datagrid.order-dir
for-each:x:/@.properties/*?name

  /*
   * Invoking property getter event, making sure we set its correct
   * name before we do.
   */
  set:x:/..for-each/*/foo?name
    src:x:/@_dp?value
  foo:x:/../*/datagrid?value
  add:x:/../*/micro.widgets.mysql.datagrid.databind
    src:x:/./-/*





/*
 * Databinding MySQL datagrid.
 */
micro.widgets.mysql.datagrid.databind:x:/../*/datagrid?value





/*
 * Making sure we signal to any listeners that datagrid has been databound.
 */
camphora.datagrid.page:x:/../*/datagrid?value
eval-x:x:/+/*
camphora.datagrid.databound:x:/../*/datagrid?value
  page:x:/@camphora.{0}.datagrid.page/*?value.long
    :{app-name}
  more-pages:x:/@micro.widgets.mysql.datagrid.databind/*/more-pages?value
  count:x:/@micro.widgets.mysql.datagrid.databind/*/count?value
