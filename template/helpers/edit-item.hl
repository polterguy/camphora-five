/*
 * File responsible for editing existing CRUD item.
 *
 * Expects a [parent], being the ID of the widget where the editing widget should
 * be injected - In addition to [id] being record's database ID.
 */





/*
 * Sanity checking argument.
 */
micro.lambda.contract.min:x:/..
  parent:string
  id:long





/*
 * Figuring out path of app's "/helpers/" folder.
 */
split:x:/..?name
  =:/
set:x:/@split/0/-
join:x:/@split/*?name
  sep:/





/*
 * Selecting item from database, and parametrizing invocation of file responsible
 * for creating our editing widget(s).
 */
p5.mysql.connect:[camphora]
  p5.mysql.select:@"select * from `{0}` where id = @id"
    :x:/@split/0/-2?name
    @id:x:/../*/id?value
  add:x:/../*/micro.evaluate.file
    src:x:/@p5.mysql.select/*/*(!/id)





/*
 * Evaluating file responsible for creating widgets for us.
 */
micro.evaluate.file:/{0}/create-item-widgets.hl
  :x:/@join?value





/*
 * Adding widgets created in above invocation to [create-widget] invocation below.
 */
add:x:/../*/create-widget/*/widgets
  src:x:/@micro.evaluate.file/*





/*
 * Adding Save and Close buttons.
 */
eval-x:x:/+/**/_app-name
add:x:/../*/create-widget/*/widgets
  src
    div
      class:right
      widgets
        div
          class:strip
          widgets
            button:camphora-save-record-button
              innerValue:@"<span class=""icon-floppy-disk""></span>"
              title:Save record
              class:large
              onclick

                /*
                 * Forward evaluated above.
                 */
                _app-name:x:/@split/0/-2?name

                /*
                 * Serializing form data, and updating database.
                 */
                p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                  .edit
                micro.form.serialize:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                get-widget-property:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                  .edit

                /*
                 * Figuring out column names for database table, and parametrizing update SQL.
                 */
                _cols
                _no:int:0
                for-each:x:/@micro.form.serialize/*
                  set:x:/@_cols?value
                    src:{0},`{1}`=@no{2}
                      :x:/@_cols?value
                      :x:/@_dp/#?name
                      :x:/@_no?value
                  eval-x:x:/+2/*/*
                  set:x:/+/*/*?name
                    src:@no{0}
                      :x:/@_no?value
                  add:x:/../*/p5.mysql.connect/*/p5.mysql.update
                    src
                      foo:x:/@_dp/#?value
                  set:x:/@_no?value
                    +:x:/@_no?value
                      _:1
                set:x:/@_cols?value
                  trim:x:/@_cols?value
                    chars:,

                /*
                 * Updating database.
                 */
                p5.mysql.connect:[camphora]
                  p5.mysql.update:@"update `{0}` set {1} where id = @id"
                    :x:/@_app-name?value
                    :x:/@_cols?value
                    @id:x:/@get-widget-property/*/*?value

                /*
                 * Re-databinding grid for updated item only.
                 */
                eval-x:x:/+/*/id
                micro.evaluate.file:/modules/{0}/helpers/databind-grid.hl
                  :x:/@_app-name?value
                  id:x:/@get-widget-property/*/*?value

                /*
                 * Deleting edit widget.
                 */
                delete-widget:x:/@p5.web.widgets.find-first-ancestor/*/*?value

            button
              innerValue:@"<span class=""icon-cross""></span>"
              title:Close editor for record
              onclick

                /*
                 * Simply deleting editing widget.
                 */
                p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                  .edit
                delete-widget:x:/-/*/*?value





/*
 * Creating editing widget for record.
 */
create-widget
  parent:x:/../*/parent?value
  class:shaded rounded air-inner air-bottom
  style:"background-color:#fefefe;"
  widgets
