
/*
 * Desktop widget file for Camphora generated apps.
 */
container
  element:a
  class:desktop-app shaded rounded air-inner
  oninit

    /*
     * Getting URL to point correctly.
     */
    get-widget-property:x:/../*/_event?value
      .app-name
    set-widget-property:x:/../*/_event?value
      href:/{0}
        :x:/@get-widget-property/*/*?value

    /*
     * Figuring out if user is allowed to view data for current app, and if not,
     * we delete the icon for the app in its entirety.
     */
    load-file:/modules/{0}/helpers/app-manifest.hl
      :x:/@get-widget-property/*/*?value
    whoami
    if:x:/@whoami/*/role?value
      !=:root
      and:x:/@whoami/*/role?value
        !=:x:/@load-file/*/*/viewers?value

      /*
       * User is not allowed to see current app's data, hence deleting desktop
       * widget in its entirety.
       */
      delete-widget:x:/../*/_event?value

    else

      /*
       * User is allowed to see data for current app, hence we continue creating
       * our desktop icon.
       */
      create-widget
        element:span
        class:desktop-app-name
        parent:x:/../*/_event?value
        innerValue:@"<span class=""icon-paragraph-justify desktop-app-icon""></span><span class=""capitalize"" style=""position:absolute;top:5px;right:5px;font-size:.7rem;"">{0}</span>"
          :x:/@get-widget-property/*/*?value
