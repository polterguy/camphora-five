/*
 * This file is responsible for actually showing the main datagrid
 * for a Camphora Five app.
 *
 * Since the _"launch.hl"_ file for a Camphora app also loads up
 * custom views, this is the actual implementation of the default
 * view, showing your datagrid, and its toolbar for paging and
 * filtering.
 *
 * This fill will include Micro's CSS files, create a Micro CSS
 * column wire frame, and instantiate the __[camphora.xxx.widgets.datagrid]__
 * and __[camphora.xxx.widgets.toolbar]__ extension widgets on your page.
 *
 * This particular file also supports the HTTP QUERY parameters of "q" and "p",
 * that allows you to supply a URL that will query ("q") or page ("p") your
 * current result set according to the given HTTP QUERY parameters.
 */





/*
 * Including Micro's CSS files, in addition to our main CSS
 * file for currently evaluated Camphora app.
 */
micro.css.include
p5.web.include-css-file:/modules/{0}/media/main.css
  :{app-name}





/*
 * Checking if we have an HTTP GET QUERY parameter, indicating initial
 * databind filtering.
 */
p5.web.query.get:q
if:x:/@p5.web.query.get/*?value

  /*
   * Adding query to widget instantiation, to make sure we initially
   * databind the widget towards the specified QUERY parameter.
   */
  eval-x:x:/+/*/*
  add:x:/../*/create-widget/**/widgets/=crud-widget/0
    src
      query:x:/@p5.web.query.get/*?value





/*
 * Checking if we have an HTTP GET QUERY parameter, indicating initial
 * page.
 */
p5.web.query.get:p
if:x:/@p5.web.query.get/*?value

  /*
   * Adding query to widget instantiation, to make sure we initially
   * databind the widget towards the specified QUERY parameter.
   */
  eval-x:x:/+/*/*
  add:x:/../*/create-widget/**/widgets/=crud-widget/0
    src
      page:x:/@p5.web.query.get/*?value





/*
 * Creating main wire frame for app, containing toolbar and datagrid.
 */
create-widget:camphora-main-container
  class:container
  events


    /*
     * Help context Active Event.
     */
    desktop.help.get-context

      /*
       * Returning the root folder for Camphora's help files.
       */
      return:@CAMPHORA/help-files/Camphora Five/index.hl


    /*
     * Invoked when for some reasons the number of items have been
     * changed in the database. The new item count will be passed in
     * as [_arg].
     *
     * We simply update the title of our page, to reflect the number
     * of items in the database.
     */
    camphora.count-changed

      /*
       * Setting the title of our page, to reflect the number of items.
       */
      p5.web.page.set-title:{0} records of type {1}
        :x:/../*/_arg?value
        :{app-name}


  widgets

    /*
     * Row wrapping all toolbars for app.
     */
    div
      class:row
      widgets
        div
          class:col-100
          widgets:toolbar-widget

            /*
             * Our actual toolbar.
             *
             * We instantiate our toolbar widget by default such that
             * it also displays the home button, delete all button, import
             * CSV file button, etc. If you don't want these buttons, or
             * this strip to be more accurate, you can set the [main-toolbar]
             * value to "false", or modify its sub-arguments to delete only
             * specific parts of the toolbar.
             *
             * Notice, the [datagrid] argument must be the widget ID of
             * your [camphora.xxx.widgets.datagrid] instance.
             */
            camphora.widgets.toolbar
              datagrid:camphora-main-datagrid
              main-toolbar:bool:true
                edit-items:bool:true
                download-items:bool:true


    /*
     * Main row wrapping our MySQL datagrid.
     */
    div
      class:row air-top
      widgets
        div
          class:col-100
          widgets:crud-widget

            /*
             * Our actual datagrid.
             *
             * We instantiate the widget such that it allows for editing
             * and deletion of items. If you don't want this feature, you
             * can change the [edit-items] value to "false".
             *
             * In addition we make sure the datagrid displays 10 items in
             * each "page". Feel free to change the [page-size] argument
             * if you want to display more or less items at the same time.
             */
            camphora.widgets.datagrid:camphora-main-datagrid
              edit-items:bool:true
              page-size:long:10
